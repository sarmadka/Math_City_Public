صنف كـلام {
    //==========
    // المتغيرات
    
    عرف اسم_ملف_الصوت: مؤشر[مصفوفة[محرف]]؛

    عرف نص: نـص؛
    عرف اسم_ملف_الصورة: نـص؛
    عرف س_الفقاعة: صحيح؛
    عرف ص_الفقاعة: صحيح؛
    عرف بعد_الفقاعة: صحيح؛
    عرف زاوية_الفقاعة: صحيح؛

    عرف س_مركز_الكتابة_المخصص: صحيح؛
    عرف ص_مركز_الكتابة_المخصص: صحيح؛

    عرف تفكير: ثنائي = 0؛

    عرف وجه: سند[مـلصق]؛
    عرف أشكال_الوجه: مصفوفة[صحيح، 2]؛

    عرف انتظار_بعدي: صحيح؛

    //=========
    // العمليات

    عملية هذا~هيئ() {}

    عملية هذا~هيئ(
        صوت: مؤشر[مصفوفة[محرف]]،
        نص: نـص، س: صحيح، ص: صحيح، بعد: صحيح، زاوية: صحيح،
        وجه: سند[مـلصق]، شكل1: صحيح، شكل2: صحيح،
        انتظار: صحيح
    ) {
        هذا.اسم_ملف_الصوت = صوت؛

        هذا.نص = نص؛
        هذا.س_الفقاعة = س؛
        هذا.ص_الفقاعة = ص؛
        هذا.بعد_الفقاعة = بعد؛
        هذا.زاوية_الفقاعة = زاوية؛
        
        هذا.وجه~مؤشر = وجه~مؤشر؛
        هذا.أشكال_الوجه(0) = شكل1؛
        هذا.أشكال_الوجه(1) = شكل2؛
        
        هذا.انتظار_بعدي = انتظار؛
    }

    عملية هذا~هيئ(
        صوت: مؤشر[مصفوفة[محرف]]،
        نص: نـص، س: صحيح، ص: صحيح، بعد: صحيح، زاوية: صحيح،
        انتظار: صحيح
    ) {
        هذا.اسم_ملف_الصوت = صوت؛

        هذا.نص = نص؛
        هذا.س_الفقاعة = س؛
        هذا.ص_الفقاعة = ص؛
        هذا.بعد_الفقاعة = بعد؛
        هذا.زاوية_الفقاعة = زاوية؛
        
        هذا.وجه~مؤشر = 0؛

        هذا.انتظار_بعدي = انتظار؛
    }

    عملية هذا~هيئ(
        صوت: مؤشر[مصفوفة[محرف]]،
        نص: نـص، س: صحيح، ص: صحيح، بعد: صحيح، زاوية: صحيح،
        تفكير: ثنائي، انتظار: صحيح
    ) {
        هذا~هيئ(صوت، نص، س، ص، بعد، زاوية، انتظار)؛
        هذا.تفكير = تفكير؛
    }

    عملية هذا~هيئ(
        صوت: مؤشر[مصفوفة[محرف]]،
        نص: نـص، س: صحيح، ص: صحيح،
        انتظار: صحيح
    ) {
        هذا.اسم_ملف_الصوت = صوت؛

        هذا.نص = نص؛
        هذا.س_الفقاعة = س؛
        هذا.ص_الفقاعة = ص؛
        هذا.بعد_الفقاعة = 0؛
        هذا.زاوية_الفقاعة = 0؛

        هذا.وجه~مؤشر = 0؛

        هذا.انتظار_بعدي = انتظار؛
    }

    عملية هذا~هيئ(
        صوت: مؤشر[مصفوفة[محرف]]، نص: نـص، اسم_ملف_الصورة: نـص،
        س: صحيح، ص: صحيح، س_مركز_الكتابة: صحيح، ص_مركز_الكتابة: صحيح،
        وجه: سند[مـلصق]، شكل1: صحيح، شكل2: صحيح،
        انتظار: صحيح
    ) {
        هذا.اسم_ملف_الصوت = صوت؛
        هذا.اسم_ملف_الصورة = اسم_ملف_الصورة؛

        هذا.نص = نص؛
        هذا.س_الفقاعة = س؛
        هذا.ص_الفقاعة = ص؛
        هذا.س_مركز_الكتابة_المخصص = س_مركز_الكتابة؛
        هذا.ص_مركز_الكتابة_المخصص = ص_مركز_الكتابة؛

        هذا.وجه~مؤشر = وجه~مؤشر؛
        هذا.أشكال_الوجه(0) = شكل1؛
        هذا.أشكال_الوجه(1) = شكل2؛

        هذا.انتظار_بعدي = انتظار؛
    }

    عملية هذا~هيئ(
        صوت: مؤشر[مصفوفة[محرف]]، نص: نـص، اسم_ملف_الصورة: نـص،
        س: صحيح، ص: صحيح، س_مركز_الكتابة: صحيح، ص_مركز_الكتابة: صحيح،
        انتظار: صحيح
    ) {
        هذا.اسم_ملف_الصوت = صوت؛
        هذا.اسم_ملف_الصورة = اسم_ملف_الصورة؛

        هذا.نص = نص؛
        هذا.س_الفقاعة = س؛
        هذا.ص_الفقاعة = ص؛
        هذا.س_مركز_الكتابة_المخصص = س_مركز_الكتابة؛
        هذا.ص_مركز_الكتابة_المخصص = ص_مركز_الكتابة؛

        هذا.وجه~مؤشر = 0؛

        هذا.انتظار_بعدي = انتظار؛
    }

    عملية هذا~هيئ(كلام:سند[كـلام]) { هذا.انسخ(كلام)؛ }

    عملية هذا = سند[كـلام] { هذا.انسخ(قيمة)؛ }

    //=======
    // الدالات
    
    عملية هذا.انسخ (كلام: سند[كـلام]) {
        هذا.اسم_ملف_الصوت = كلام.اسم_ملف_الصوت؛
        هذا.نص = كلام.نص؛
        هذا.اسم_ملف_الصورة = كلام.اسم_ملف_الصورة؛
        هذا.س_الفقاعة = كلام.س_الفقاعة؛
        هذا.ص_الفقاعة = كلام.ص_الفقاعة؛
        هذا.بعد_الفقاعة = كلام.بعد_الفقاعة؛
        هذا.زاوية_الفقاعة = كلام.زاوية_الفقاعة؛
        هذا.س_مركز_الكتابة_المخصص = كلام.س_مركز_الكتابة_المخصص؛
        هذا.ص_مركز_الكتابة_المخصص = كلام.ص_مركز_الكتابة_المخصص؛
        هذا.وجه~مؤشر = كلام.وجه~مؤشر؛
        هذا.أشكال_الوجه = كلام.أشكال_الوجه؛
        هذا.انتظار_بعدي = كلام.انتظار_بعدي؛
        هذا.تفكير = كلام.تفكير؛
    }

    عملية هذا.شغل (مشهد: سند[مـشهد]) {
        عرف صوت: صـوت؛
        انتظر_مع_التحديث(صوت.هيئ(نـص(هذا.اسم_ملف_الصوت)))؛
        عرف الحالة_السابقة:صحيح؛
        عرف الشفافية_السابقة:صحيح؛
        إذا هذا.وجه~مؤشر != 0 {
            الحالة_السابقة = هذا.وجه.الحالي؛
            الشفافية_السابقة = هذا.وجه.شفافية؛
        }
        
        عرف فقاعة: مـلصق؛
        إذا هذا.اسم_ملف_الصورة != "" فقاعة = انتظر_مع_التحديث[مـلصق](أنشئ_فقاعة_كلام_مخصصة(
            هذا.نص، هذا.اسم_ملف_الصورة، هذا.س_الفقاعة، هذا.ص_الفقاعة، هذا.س_مركز_الكتابة_المخصص، هذا.ص_مركز_الكتابة_المخصص
        ))
        وإلا إذا هذا.بعد_الفقاعة == 0 فقاعة = أنشئ_نافذة_كلام(هذا.نص، هذا.س_الفقاعة، هذا.ص_الفقاعة)
        وإلا إذا هذا.تفكير فقاعة = أنشئ_فقاعة_تفكير(هذا.نص، هذا.س_الفقاعة، هذا.ص_الفقاعة، هذا.زاوية_الفقاعة، هذا.بعد_الفقاعة)
        وإلا فقاعة = أنشئ_فقاعة_كلام(هذا.نص، هذا.س_الفقاعة، هذا.ص_الفقاعة، هذا.زاوية_الفقاعة، هذا.بعد_الفقاعة)؛
        مشهد.أضف_ملصق(فقاعة)؛
        إذا هذا.وجه~مؤشر != 0 هذا.وجه.شفافية = 255؛
        صوت.شغل(0)؛
        بينما صوت.أمشتغل() {
            إذا هذا.وجه~مؤشر != 0 هذا.وجه.الحالي = هذا.أشكال_الوجه(مـتم.ريـاضيات.عشوائي() % 2)؛
            حدث()؛
            إذا القائمة_العائمة.التجاوز_مفعل أو القائمة_العائمة.العودة_مفعلة اقطع؛
            نـظام.نم(100000)؛
        }
        إذا هذا.وجه~مؤشر != 0 {
            هذا.وجه.شفافية = الشفافية_السابقة؛
            هذا.وجه.الحالي = الحالة_السابقة؛
        }
        فقاعة.شفافية = 0؛

        تمهل(هذا.انتظار_بعدي)؛
    }
}

عرف مـصفوفة_كلام: لقب مـصفوفة[كـلام]؛

