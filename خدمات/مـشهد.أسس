صنف مـشهد {
    //==========
    // المتغيرات

    @مشترك عرف المشاهد: مـصفوفة[سند[مـشهد]]؛
    عرف ملصقات: مـصفوفة[سند[مـلصق]]؛
    عرف نقش_الخلفية: نـقش_متعدد؛
    عرف شفافية: صحيح = 255؛
    عرف عمق: صـحيح؛
    عرف تحديث: مغلفة() = مغلفة() {}؛


    //=========
    // العمليات
    
    عملية هذا~هيئ() {
        هذا.عمق = 0؛
        هذا.أضف_للمصفوفة()؛
    }

    عملية هذا~هيئ(عمق: صـحيح) {
        هذا.عمق = عمق؛
        هذا.أضف_للمصفوفة()؛
    }
    
    عملية هذا~أتلف() {
        هذا.أزل_من_المصفوفة()؛
        هذا.حرر()؛
    }

    //=======
    // الدالات

    عملية هذا.أضف_للمصفوفة() {
        عرف ع: صـحيح؛
        لكل ع = 0، ع < المشاهد.هات_الطول()، ++ع {
            إذا هذا.عمق < المشاهد(ع).عمق {
                المشاهد.احشر(ع، هذا)؛
                ارجع؛
            }
        }
        المشاهد.أضف(هذا)؛
   }

    عملية هذا.أزل_من_المصفوفة() {
         عرف ن: صحيح؛
        لكل ن = 0، ن < المشاهد.هات_الطول()، ++ن {
            إذا المشاهد(ن)~مؤشر == هذا~مؤشر {
                المشاهد.أزل(ن)؛
                اقطع؛
            }
        }
   }

    عملية هذا.حمل_خلفية (اسم_الملف: نـص، تحجيم: صـحيح): سـندنا[مـؤجلات.مـؤجلة[صـحيح]] {
        أرجع هذا.نقش_الخلفية.هيئ_مفرد(اسم_الملف، تحجيم)؛
    }

    عملية هذا.حرر() {
        هذا.نقش_الخلفية.حرر()؛
        عرف ع: صـحيح؛
        لكل ع = 0، ع < هذا.ملصقات.هات_الطول()، ++ع هذا.ملصقات(ع).مشهد~مؤشر = 0؛
        هذا.ملصقات.فرغ()؛
    }
    
    عملية هذا.أضف_ملصق (ملصق: سند[مـلصق]) {
        إذا ملصق.مشهد~مؤشر != 0 {
            ملصق.مشهد.أزل_ملصق(ملصق)؛
        }
        هذا.ملصقات.أضف(ملصق)؛
        ملصق.مشهد~مؤشر = هذا~مؤشر؛
    }
    
    عملية هذا.أضف_ملصقات (ملصقات: سند[مـصفوفة[مـلصق]]) {
        عرف م: صحيح؛
        لكل م = 0، م < ملصقات.هات_الطول()، ++م هذا.أضف_ملصق(ملصقات(م))؛
    }

    عملية هذا.أضف_ملصقات (عدد: صحيح، ملصقات: ...سند[مـلصق]) {
        بينما عدد-- > 0 هذا.أضف_ملصق(ملصقات~المعطى_التالي[سند[مـلصق]])؛
    }

    عملية هذا.أزل_ملصق (ملصق: سند[مـلصق]) {
        عرف ن: صحيح؛
        لكل ن = 0، ن < هذا.ملصقات.هات_الطول()، ++ن {
            إذا هذا.ملصقات(ن)~مؤشر == ملصق~مؤشر {
                ملصق.مشهد~مؤشر = 0؛
                هذا.ملصقات.أزل(ن)؛
                ارجع؛
            }
        }
    }
    
    عملية هذا.أزل_ملصقات (عدد: صحيح، ملصقات: ...سند[مـلصق]) {
        بينما عدد-- > 0 هذا.أزل_ملصق(ملصقات~المعطى_التالي[سند[مـلصق]])؛
    }
    
    عملية هذا.ارسم() {
        إذا هذا.شفافية == 0 ارجع؛
        إذا هذا.نقش_الخلفية.أمهيأ() {
            هذا.نقش_الخلفية.اطمغ(0، 0، هذا.شفافية، 0)؛
        }
        عرف ن: صحيح؛
        لكل ن = 0، ن < هذا.ملصقات.هات_الطول()، ++ن {
            هذا.ملصقات(ن).ارسم(هذا.شفافية)؛
        }
    }

    عملية هذا.حدث() {
        هذا.تحديث()؛
    }
}
