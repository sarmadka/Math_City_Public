//============================
// المتغيرات والثوابت العمومية

عرف سواد: مـلصق؛
عرف هل_نحتاج_لوحة_المفاتيح : ثنائي = 0؛
عرف نافذة_إعادة_المحاولة: مـلصق؛
عرف إظهار_نافذة_إعادة_المحاولة: ثـنائي(0)؛
عرف مؤجلة_نافذة_إعادة_المحاولة: سـندنا[مـؤجلات.مـؤجلة[صـحيح]]؛

//================
// الدالات العمومية

دالة جهز_السواد {
    سواد.هيئ(8، 8)؛
    ابدأ_رسم_النقش(سواد، 1)؛
    عين_لون_الرسم(0، 0، 0، 255)؛
    ارسم_مستطيلا_مليئا(0، 0، 7، 7)؛
    أنه_رسم_النقش()؛
    سواد.نقش.عرض = _عرض_النافذة_؛
    سواد.نقش.طول = _طول_النافذة_؛
}


دالة جهز_نافذة_إعادة_المحاولة {
    عرف نص_الإشعار: مـصفوفة[نـص] = نـص(
        "يوجد مشكلة في الاتصال مع الخادم.\جتأكد من اتصال جهازك بالشبكة ثم أعد المحاولة."
    ).قطع("\ج")؛
    عرف نص_الزر: مـصفوفة[نـص]({ نـص("انقر لإعادة المحاولة") })؛

    // احسب حجم الكتابة.
    عرف ع: صحيح؛
    عرف ط: صحيح؛
    احسب_حجم_الكتابة(نص_الإشعار، ع، ط)؛
    عرف ع_الزر: صـحيح؛
    عرف ط_الزر: صـحيح؛
    احسب_حجم_الكتابة(نص_الزر، ع_الزر، ط_الزر)؛

    // احسب حجم الملصق ومركزه.
    عرف ملصق_ع: صحيح = ع + 20؛
    عرف ملصق_ط: صحيح = ط + ط_الزر + 23؛
    عرف مركز_س: صحيح = ملصق_ع ÷ 2؛
    عرف مركز_ص: صحيح = ط ÷ 2 + 10؛

    نافذة_إعادة_المحاولة.هيئ(ملصق_ع، ملصق_ط)؛
    ابدأ_رسم_النقش(نافذة_إعادة_المحاولة، 1)؛

    // ارسم نافذة الكلام.
    عرف نقاط: مـصفوفة[صحيح]({
        0، 0،
        ملصق_ع - 1، 0،
        ملصق_ع - 1، ملصق_ط - 1،
        0، ملصق_ط - 1،
        0، 0
    })؛
    ارسم_شكلا_مؤطرا(نقاط)؛
    اكتب_سطور(نص_الإشعار، مركز_س + ع÷2، مركز_ص - ط÷2)؛
    اكتب_سطور(نص_الزر، مركز_س + ع_الزر÷2، مركز_ص + ط÷2 + 10، لـون(0، 128، 255، 255))؛

    أنه_رسم_النقش()؛

    نافذة_إعادة_المحاولة.ضع(_عرض_النافذة_÷2 - ملصق_ع÷2، _طول_النافذة_÷2 - ملصق_ط÷2)؛
}


دالة اعرض_نافذة_إعادة_المحاولة(): سـندنا[مـؤجلات.مـؤجلة[صـحيح]] {
    إذا ليس مؤجلة_نافذة_إعادة_المحاولة.أهو_عدم() أرجع مؤجلة_نافذة_إعادة_المحاولة؛
    مؤجلة_نافذة_إعادة_المحاولة = مـؤجلات.مـؤجلة[صـحيح].أنشئ()؛
    إظهار_نافذة_إعادة_المحاولة = 1؛
    ارسم_النافذة()؛
    تحكم.عند_النقر = مغلفة () {
        تحكم.عند_النقر.حرر()؛
        مؤجلة_نافذة_إعادة_المحاولة.قرر(0)؛
        مؤجلة_نافذة_إعادة_المحاولة.حرر()؛
        إظهار_نافذة_إعادة_المحاولة = 0؛
        ارسم_النافذة()؛
    }؛
    أرجع مؤجلة_نافذة_إعادة_المحاولة؛
}


دالة حمل_ملصقا(بادئة: نـص، تحجيم: صـحيح): سـندنا[مـؤجلات.مـؤجلة[مـلصق]] {
    عرف نقش: سـندنا[نـقش_متعدد]؛
    نقش.أنشئ()؛
    أرجع نقش.هيئ(بادئة، تحجيم).ثم[مـلصق](مغلفة (صـحيح، م: سند[مـؤجلات.مـؤجلة[مـلصق]]) {
        م.قرر(مـلصق(نقش))؛
    })؛
}


دالة اعرض_محادثة (مشهد: سند[مـشهد]، محادثة: سند[مـصفوفة[كـلام]]) {
    عرف ن: صحيح؛
    لكل ن = 0، ن < محادثة.هات_الطول()، ++ن {
        محادثة(ن).شغل(مشهد)؛
        إذا القائمة_العائمة.التجاوز_مفعل أو القائمة_العائمة.العودة_مفعلة اقطع؛
    }
}؛


دالة غير_الشفافية_رويدا (ملصق: سند[مـلصق]، بداية: صحيح، نهاية: صحيح، ميلي_ثواني: صحيح[64]) {
    عرف وقت_الانطلاق: صحيح[64] = هات_الختم_الزمني_بالملي()؛
    بينما 1 {
        عرف الآن: صحيح[64] = هات_الختم_الزمني_بالملي()؛
        إذا الآن - وقت_الانطلاق >= ميلي_ثواني اقطع؛
        ملصق.شفافية = بداية + (نهاية - بداية) * (الآن - وقت_الانطلاق) ÷ ميلي_ثواني؛
        حدث()؛
    }
    ملصق.شفافية = نهاية؛
    حدث()؛
}


دالة هات_عدد_المراتب (ن:صحيح):صحيح {
    عرف عدد_المراتب : صحيح = 1؛
    عرف قوة : صحيح = 10؛    
    
    بينما قوة <= ن {
        ++عدد_المراتب؛
        قوة *= 10؛
    }

    ارجع (عدد_المراتب)؛
}


دالة أظهر_رويدا (مشهد: سند[مـشهد]، ميلي_ثواني: صحيح[64]) {
    عرف مشهد_سواد: مـشهد(200)؛
    مشهد_سواد.أضف_ملصق(سواد)؛
    مشهد.شفافية = 255؛
    غير_الشفافية_رويدا(سواد، 255، 0، ميلي_ثواني)؛
}


دالة أخف_رويدا (مشهد: سند[مـشهد]، ميلي_ثواني: صحيح[64]) {
    عرف مشهد_سواد: مـشهد(200)؛
    مشهد_سواد.أضف_ملصق(سواد)؛
    غير_الشفافية_رويدا(سواد، 0، 255، ميلي_ثواني)؛
    مشهد.شفافية = 0؛
}


دالة ابدأ_رسم_النقش (ملصق: سند[مـلصق]، نظف: ثنائي) {
    ابدأ_رسم_النقش(ملصق، نظف، 0)؛
}


دالة ابدأ_رسم_النقش (ملصق: سند[مـلصق]، نظف: ثنائي، حالة: صحيح) {
    ابدأ_رسم_النقش(ملصق.نقش، نظف، حالة)؛
}
