صنف قـائمة_عائمة {
    عرف المشهد: مـشهد(100)؛
    عرف زر_العودة: مـلصق؛
    عرف إشعار_العودة: مـلصق؛
    عرف زر_التجاوز: مـلصق؛
    عرف إشعار_التجاوز: مـلصق؛
    عرف العودة_مفعلة: ثـنائي = 0؛
    عرف التجاوز_مفعل: ثـنائي = 0؛
    عرف الحالة: صـحيح = 0؛
    عرف بداية_الكبسة: صـحيح[64] = 0؛
    عرف _طول_كبسة_العودة_: 1500؛
    عرف _طول_كبسة_التجاوز_: 750؛

    عملية هذا.هيئ(): سـندنا[مـؤجلات.مـؤجلة[صـحيح]] {
        أرجع مـؤجلات.مـؤجلة[صـحيح].الكل({
            هذا.زر_العودة.هيئ(نـص("رسوم/لوح_مفاتيح/عودة")، 0، 0، 0)،
            هذا.إشعار_العودة.هيئ(نـص("رسوم/لوح_مفاتيح/إشعار_العودة")، 0)،
            هذا.زر_التجاوز.هيئ(نـص("رسوم/لوح_مفاتيح/تجاوز")، 0، 0، _طول_النافذة_ - 120)،
            هذا.إشعار_التجاوز.هيئ(نـص("رسوم/لوح_مفاتيح/إشعار_التجاوز")، 0)،
        }).ثم[صـحيح](مغلفة (مـصفوفة[صـحيح]، م: سند[مـؤجلات.مـؤجلة[صـحيح]]) {
            هذا.المشهد.أضف_ملصقات({ هذا.زر_العودة، هذا.إشعار_العودة، هذا.زر_التجاوز، هذا.إشعار_التجاوز })؛
            هذا.إشعار_العودة.ضع(_عرض_النافذة_ ÷ 2 - هذا.إشعار_العودة.عرض ÷ 2، _طول_النافذة_ - هذا.إشعار_العودة.طول - 10)؛
            هذا.إشعار_العودة.شفافية = 0؛
            هذا.إشعار_التجاوز.ضع(_عرض_النافذة_ ÷ 2 - هذا.إشعار_التجاوز.عرض ÷ 2، _طول_النافذة_ - هذا.إشعار_التجاوز.طول - 10)؛
            هذا.إشعار_التجاوز.شفافية = 0؛
            هذا.هيئ_تحديث_المشهد()؛
            م.قرر(0)؛
        })؛
    }

    عملية هذا.هيئ_تحديث_المشهد() {
        هذا.المشهد.تحديث = مغلفة() {
            إذا تحكم.نقرة_للتو {
                إذا هذا.ضمن_زر_العودة() {
                    هذا.بداية_الكبسة = هات_الختم_الزمني_بالملي()؛
                    هذا.الحالة = 1؛
                } وإلا إذا هذا.ضمن_زر_التجاوز() {
                    هذا.بداية_الكبسة = هات_الختم_الزمني_بالملي()؛
                    هذا.الحالة = 2؛
                } وإلا {
                    هذا.الحالة = 0؛
                }
            }
            إذا تحكم.نقرة {
                إذا هذا.ضمن_زر_العودة() و هذا.الحالة == 1 {
                    إذا هات_الختم_الزمني_بالملي() - هذا.بداية_الكبسة >= _طول_كبسة_العودة_ {
                        هذا.الحالة = 0؛
                        هذا.العودة_مفعلة = 1؛
                        حدد_موضع_التشغيل_الرئيسي(نـص("البداية"))؛
                    }
                } وإلا إذا هذا.ضمن_زر_التجاوز() و هذا.الحالة == 2 {
                    إذا هات_الختم_الزمني_بالملي() - هذا.بداية_الكبسة >= _طول_كبسة_التجاوز_ {
                        هذا.الحالة = 0؛
                        هذا.التجاوز_مفعل = 1؛
                    }
                } وإلا {
                    هذا.الحالة = 0؛
                }
            } وإلا هذا.الحالة = 0؛

            إذا هذا.الحالة == 0 {
                هذا.إشعار_العودة.شفافية = 0؛
                هذا.إشعار_التجاوز.شفافية = 0؛
            } وإلا إذا هذا.الحالة == 1 {
                هذا.إشعار_العودة.شفافية = 255؛
                هذا.إشعار_التجاوز.شفافية = 0؛
            } وإلا {
                هذا.إشعار_العودة.شفافية = 0؛
                هذا.إشعار_التجاوز.شفافية = 255؛
            }
        }؛
    }

    عملية هذا.ضمن_زر_العودة(): ثـنائي {
        إذا هذا.زر_العودة.شفافية == 0 أرجع 0؛
        أرجع تحكم.س >= هذا.زر_العودة.س و تحكم.س < هذا.زر_العودة.س + هذا.زر_العودة.عرض
            و تحكم.ص >= هذا.زر_العودة.ص و تحكم.ص < هذا.زر_العودة.ص + هذا.زر_العودة.طول؛
    }

    عملية هذا.ضمن_زر_التجاوز(): ثـنائي {
        إذا هذا.زر_التجاوز.شفافية == 0 أرجع 0؛
        أرجع تحكم.س >= هذا.زر_التجاوز.س و تحكم.س < هذا.زر_التجاوز.س + هذا.زر_التجاوز.عرض
            و تحكم.ص >= هذا.زر_التجاوز.ص و تحكم.ص < هذا.زر_التجاوز.ص + هذا.زر_التجاوز.طول؛
    }

    عملية هذا.مكن_التجاوز() {
        هذا.زر_العودة.شفافية = 255؛
        هذا.زر_التجاوز.شفافية = 255؛
        هذا.العودة_مفعلة = 0؛
        هذا.التجاوز_مفعل = 0؛
    }

    عملية هذا.مكن_العودة() {
        هذا.زر_العودة.شفافية = 255؛
        هذا.زر_التجاوز.شفافية = 0؛
        هذا.العودة_مفعلة = 0؛
        هذا.التجاوز_مفعل = 0؛
    }

    عملية هذا.عطل() {
        هذا.زر_العودة.شفافية = 0؛
        هذا.زر_التجاوز.شفافية = 0؛
        هذا.العودة_مفعلة = 0؛
        هذا.التجاوز_مفعل = 0؛
    }
}
