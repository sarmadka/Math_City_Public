@جدول["users"]
صنف مـستخدم {
    صـفوف.عرف_أساسيات_الجدول[]؛

    @فهرس_رئيسي
    @مـحارف_مرنة["250"]
    @حقل["id"]
    عرف المعرف: نـص؛

    @مـحارف_مرنة["250"]
    @حقل["name"]
    عرف الاسم: نـص؛

    @مـحارف_مرنة["250"]
    @حقل["email"]
    عرف البريد_الإلكتروني: نـص؛

    @ثـنائية
    @إلزامي
    @مبدئي["0"]
    @حقل["subscribed"]
    عرف مشترك: ثـنائي؛

    @تـاريخ_ووقت
    @إلزامي
    @حقل["created_at"]
    عرف تاريخ_الإنشاء: نـص؛

    @تـاريخ_ووقت
    @حقل["updated_at"]
    عرف تاريخ_التحديث: بـعدم[نـص]؛
}

@جدول["payment_sessions"]
صنف جـلسة_دفع {
    صـفوف.عرف_أساسيات_الجدول[]؛

    عرف حـالة: {
        عرف _جديد_: "new"؛
        عرف _مكتمل_: "complete"؛
    }

    @فهرس_رئيسي
    @مـحارف_مرنة["250"]
    @حقل["id"]
    عرف المعرف: نـص؛

    @فهرس_وصل["users"، "id"]
    @مـحارف_مرنة["250"]
    @حقل["user_id"]
    عرف معرف_المستخدم: نـص؛

    @مـحارف_مرنة["50"]
    @حقل["status"]
    عرف الحالة: نـص؛

    @تـاريخ_ووقت
    @إلزامي
    @حقل["created_at"]
    عرف تاريخ_الإنشاء: نـص؛

    @تـاريخ_ووقت
    @حقل["updated_at"]
    عرف تاريخ_التحديث: بـعدم[نـص]؛
}

عرف قب: صـفوف.قـاعدة_بيانات؛

دالة هيئ_قاعدة_البيانات {
    قب.هيئ(صـفوف.مـشغل_بوستغرس(صـفوف.مـعطيات_الاتصال().{
        اسم_قاعدة_البيانات = إعـدادات.اسم_قاعدة_البيانات؛
        اسم_المستخدم = إعـدادات.اسم_مستخدم_قاعدة_البيانات؛
        كلمة_السر = إعـدادات.كلمة_سر_قاعدة_البيانات؛
        عنوان_الخادم = إعـدادات.عنوان_خادم_قاعدة_البيانات؛
        المنفذ = إعـدادات.منفذ_قاعدة_البيانات؛
    }))؛
    إذا !قب.أمتصل() {
        نـظام.فشل(1، نـص("فشل الاتصال بقاعدة البيانات: ") + قب.هات_آخر_خطأ())؛
    }

    قب.مهيكل[مـستخدم].أنشئ()؛
    قب.مهيكل[جـلسة_دفع].أنشئ()؛
}

دالة هات_المستخدم (جواب: هـوية.جـواب_مصادقة): نـص {
    عرف معرف: نـص = جواب.اسم_المزود + "$" + جواب.المعرف؛
    عرف م: مـصفوفة[سـندنا[مـستخدم]] = قب.من[مـستخدم].حيثما[المعرف == معرف].اجلب()؛
    إذا م.هات_الطول() == 0 {
        عرف مستخدم: مـستخدم؛
        مستخدم.{
            المعرف = معرف؛
            الاسم = جواب.الاسم؛
            البريد_الإلكتروني = جواب.البريد_الإلكتروني؛
            مشترك = 0؛
            تاريخ_الإنشاء = هات_التاريخ_الحالي()؛
        }؛
        قب.احفظ[مـستخدم](مستخدم)؛
    } وإلا {
        م(0).{
            الاسم = جواب.الاسم؛
            البريد_الإلكتروني = جواب.البريد_الإلكتروني؛
            تاريخ_التحديث = هات_التاريخ_الحالي()؛
        }؛
        م(0).احفظ()؛
    }
    أرجع معرف؛
}

دالة هات_المستخدم (معرف: نـص): سـندنا[مـستخدم] {
    عرف م: مـصفوفة[سـندنا[مـستخدم]] = قب.من[مـستخدم].حيثما[المعرف == معرف].اجلب()؛
    إذا م.هات_الطول() != 0 {
        أرجع م(0)؛
    } وإلا {
        أرجع سـندنا[مـستخدم]()؛
    }
}

دالة أنشئ_جلسة_دفع (معرف_مستخدم: نـص، معرف_الجلسة: نـص): سـندنا[جـلسة_دفع] {
    عرف جلسة_دفع: سـندنا[جـلسة_دفع]؛
    جلسة_دفع.أنشئ()؛
    جلسة_دفع.{
        المعرف = معرف_الجلسة؛
        معرف_المستخدم = معرف_مستخدم؛
        الحالة = جـلسة_دفع.حـالة._جديد_؛
        تاريخ_الإنشاء = هات_التاريخ_الحالي()؛
    }؛
    قب.احفظ[جـلسة_دفع](جلسة_دفع)؛
    أرجع جلسة_دفع؛
}

دالة هات_جلسة_الدفع_النشطة (معرف_مستخدم: نـص): سـندنا[جـلسة_دفع] {
    عرف م: مـصفوفة[سـندنا[جـلسة_دفع]] = قب
        .من[جـلسة_دفع]
        .حيثما[معرف_المستخدم == معرف_مستخدم و الحالة == جـلسة_دفع.حـالة._جديد_]
        .اجلب()؛
    إذا م.هات_الطول() != 0 {
        أرجع م(0)؛
    } وإلا {
        أرجع سـندنا[جـلسة_دفع]()؛
    }
}
