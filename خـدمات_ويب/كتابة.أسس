دالة أنشئ_فقاعة_كلام (نص: نـص، س: صحيح، ص: صحيح، زاوية: صحيح، بعد: صحيح): مـلصق {
    عرف سطور: مـصفوفة[نـص] = نص.قطع("\ج")؛

    // احسب حجم الكتابة.
    عرف ع: صحيح؛
    عرف ط: صحيح؛
    احسب_حجم_الكتابة(سطور، ع، ط)؛

    // احسب حجم الفقاعة.
    عرف _نسبة_: 1.1؛
    عرف نقع: صحيح = _نسبة_ * ع ÷ 1.414235؛
    عرف نقط: صحيح = _نسبة_ * ط ÷ 1.414235؛

    // احسب زاوية الامتداد
    عرف _النسبة_الثابتة_: 3.141592f64؛
    عرف ز: عائم[64] = _النسبة_الثابتة_ + (زاوية * _النسبة_الثابتة_ ÷ 180.0f64)؛

    // احسب بعد مركز الفقاعة عن مصدر الكلام.
    عرف بعد_س: صحيح = (نقع + بعد) * ريـاضيات.جتا(ز)؛
    عرف بعد_ص: صحيح = -(نقط + بعد) * ريـاضيات.جا(ز)؛

    // احسب حجم الملصق واحداثيات مركز الفقاعة.
    عرف ملصق_ع: صحيح؛
    عرف ملصق_ط: صحيح؛
    عرف مركز_س: صحيح؛
    عرف مركز_ص: صحيح؛
    إذا ريـاضيات.مطلق(بعد_س) <= نقع {
        ملصق_ع = نقع * 2 + 4؛
        مركز_س = نقع + 4؛
    } وإلا {
        ملصق_ع = ريـاضيات.مطلق(بعد_س) + نقع + 4؛
        إذا بعد_س < 0 مركز_س = -بعد_س وإلا مركز_س = نقع؛
    }
    إذا ريـاضيات.مطلق(بعد_ص) <= نقط {
        ملصق_ط = نقط * 2 + 4؛
        مركز_ص = نقط + 4؛
    } وإلا {
        ملصق_ط = ريـاضيات.مطلق(بعد_ص) + نقط + 4؛
        إذا بعد_ص < 0 مركز_ص = -بعد_ص وإلا مركز_ص = نقط؛
    }

    // كبر الملصق قليلا للحالات غير المتوقعة.
    ملصق_ع += 32؛
    ملصق_ط += 32؛
    مركز_س += 16؛
    مركز_ص += 16؛

    // احسب زاوية بداية قوس الفقاعة.
    عرف _زاوية_النتوء_: 0.12f64؛
    عرف زاوية_بداية_النتوء: عائم[64] = ز + _زاوية_النتوء_؛

    // جهز إحداثيات الرسم.
    عرف نقاط: مـصفوفة[صحيح]({ مركز_س + بعد_س، مركز_ص + بعد_ص })؛
    عرف خطوة: عائم[64] = (_النسبة_الثابتة_ * 2 - _زاوية_النتوء_ * 2) ÷ 32.0f64؛
    عرف ه: صحيح؛
    لكل (ز = زاوية_بداية_النتوء، ه = 0)، ه < 32، (++ه، ز += خطوة) {
        نقاط.أضف({ مركز_س + ريـاضيات.جتا(ز) * نقع، مركز_ص - ريـاضيات.جا(ز) * نقط })؛
    }
    نقاط.أضف({ مركز_س + بعد_س، مركز_ص + بعد_ص })؛

    // أنشئ الفقاعة.
    عرف فقاعة: مـلصق(ملصق_ع، ملصق_ط)؛
    فقاعة.ضع(س - مركز_س - بعد_س، ص - مركز_ص - بعد_ص)؛
    ابدأ_رسم_النقش(فقاعة، 1)؛
    ارسم_شكلا_مؤطرا(نقاط)؛
    اكتب_سطور(سطور، مركز_س + ع÷2، مركز_ص - ط÷2)؛
    أنه_رسم_النقش()؛

    أرجع فقاعة؛
}


دالة أنشئ_فقاعة_كلام_مخصصة (
    نص: نـص، اسم_ملف_الصورة: نـص، س: صحيح، ص: صحيح، س_مركز_الكتابة: صحيح، ص_مركز_الكتابة: صحيح
): سـندنا[مـؤجلات.مـؤجلة[مـلصق]] {
    عرف نقش: سـندنا[نـقش_متعدد]؛
    نقش.أنشئ()؛
    أرجع نقش.هيئ(اسم_ملف_الصورة، 0).ثم[مـلصق](مغلفة (صـحيح، م: سند[مـؤجلات.مـؤجلة[مـلصق]]) {
        عرف فقاعة: مـلصق(نقش)؛
        عرف سطور: مـصفوفة[نـص] = نص.قطع("\ج")؛

        // احسب حجم الكتابة.
        عرف ع: صحيح؛
        عرف ط: صحيح؛
        احسب_حجم_الكتابة(سطور، ع، ط)؛

        // اكتب السطور.
        ابدأ_رسم_النقش(فقاعة، 0)؛
        اكتب_سطور(سطور، س_مركز_الكتابة + ع÷2، ص_مركز_الكتابة - ط÷2)؛
        أنه_رسم_النقش()؛

        فقاعة.ضع(س، ص)؛
        م.قرر(فقاعة)؛
    })؛
}


دالة أنشئ_فقاعة_تفكير (نص: نـص، س: صحيح، ص: صحيح، زاوية: صحيح، بعد: صحيح): مـلصق {
    عرف سطور: مـصفوفة[نـص] = نص.قطع("\ج")؛

    // احسب حجم الكتابة.
    عرف ع: صحيح؛
    عرف ط: صحيح؛
    احسب_حجم_الكتابة(سطور، ع، ط)؛

    // احسب حجم الفقاعة.
    عرف _نسبة_: 1.1؛
    عرف نقع: صحيح = _نسبة_ * ع ÷ 1.414235؛
    عرف نقط: صحيح = _نسبة_ * ط ÷ 1.414235؛

    // احسب زاوية الامتداد
    عرف _النسبة_الثابتة_: 3.141592f64؛
    عرف ز: عائم[64] = _النسبة_الثابتة_ + (زاوية * _النسبة_الثابتة_ ÷ 180.0f64)؛

    // احسب بعد مركز الفقاعة عن مصدر الكلام.
    عرف بعد_س: صحيح = (نقع + بعد) * ريـاضيات.جتا(ز)؛
    عرف بعد_ص: صحيح = -(نقط + بعد) * ريـاضيات.جا(ز)؛

    // احسب حجم الملصق واحداثيات مركز الفقاعة.
    عرف ملصق_ع: صحيح؛
    عرف ملصق_ط: صحيح؛
    عرف مركز_س: صحيح؛
    عرف مركز_ص: صحيح؛
    إذا ريـاضيات.مطلق(بعد_س) <= نقع {
        ملصق_ع = نقع * 2 + 4؛
        مركز_س = نقع + 4؛
    } وإلا {
        ملصق_ع = ريـاضيات.مطلق(بعد_س) + نقع + 4؛
        إذا بعد_س < 0 مركز_س = -بعد_س وإلا مركز_س = نقع؛
    }
    إذا ريـاضيات.مطلق(بعد_ص) <= نقط {
        ملصق_ط = نقط * 2 + 4؛
        مركز_ص = نقط + 4؛
    } وإلا {
        ملصق_ط = ريـاضيات.مطلق(بعد_ص) + نقط + 4؛
        إذا بعد_ص < 0 مركز_ص = -بعد_ص وإلا مركز_ص = نقط؛
    }

    // كبر الملصق قليلا للحالات غير المتوقعة.
    ملصق_ع += 32؛
    ملصق_ط += 32؛
    مركز_س += 16؛
    مركز_ص += 16؛

    عرف خطوة: عائم[64] = (_النسبة_الثابتة_ * 2) ÷ 32.0f64؛

    عرف فقاعة: مـلصق(ملصق_ع، ملصق_ط)؛
    ابدأ_رسم_النقش(فقاعة، 1)؛

    عرف نقاط: مـصفوفة[صحيح]()؛

    // ارسم الفقيعات.
    عرف ه: صحيح؛
    لكل ه = 0، ه < 2، ++ه {
        عرف مركز_فقيعة_س: صحيح = مركز_س + بعد_س - (10 + ه * بعد * 0.5) * ريـاضيات.جتا(ز)؛
        عرف مركز_فقيعة_ص: صحيح = مركز_ص + بعد_ص + (10 + ه * بعد * 0.5) * ريـاضيات.جا(ز)؛
        نقاط.فرغ()؛
        عرف ه2: صحيح؛
        لكل ه2 = 0، ه2 < 32، (++ه2، ز += خطوة) {
            نقاط.أضف({
                مركز_فقيعة_س + ريـاضيات.جتا(ز) * (8 + ه * بعد * 0.15)،
                مركز_فقيعة_ص - ريـاضيات.جا(ز) * (8 + ه * بعد * 0.15)
            })؛
        }
        نقاط.أضف({
            مركز_فقيعة_س + ريـاضيات.جتا(ز) * (8 + ه * بعد * 0.15)،
            مركز_فقيعة_ص - ريـاضيات.جا(ز) * (8 + ه * بعد * 0.15)
        })؛
        ارسم_شكلا_مؤطرا(نقاط)؛
    }

    // ارسم فقاعة الكلام.
    نقاط.فرغ()؛
    لكل ه = 0، ه < 32، (++ه، ز += خطوة) {
        نقاط.أضف({ مركز_س + ريـاضيات.جتا(ز) * نقع، مركز_ص - ريـاضيات.جا(ز) * نقط })؛
    }
    نقاط.أضف({ مركز_س + ريـاضيات.جتا(ز) * نقع، مركز_ص - ريـاضيات.جا(ز) * نقط })؛

    // ارسم الفقاعة.
    ارسم_شكلا_مؤطرا(نقاط)؛
    اكتب_سطور(سطور، مركز_س + ع÷2، مركز_ص - ط÷2)؛

    أنه_رسم_النقش()؛

    فقاعة.ضع(س - مركز_س - بعد_س، ص - مركز_ص - بعد_ص)؛

    أرجع فقاعة؛
}


دالة أنشئ_نافذة_كلام (نص: نـص، س: صحيح، ص: صحيح): مـلصق {
    عرف سطور: مـصفوفة[نـص] = نص.قطع("\ج")؛

    // احسب حجم الكتابة.
    عرف ع: صحيح؛
    عرف ط: صحيح؛
    احسب_حجم_الكتابة(سطور، ع، ط)؛

    // احسب حجم الملصق ومركزه.
    عرف ملصق_ع: صحيح = ع + 20؛
    عرف ملصق_ط: صحيح = ط + 20؛
    عرف مركز_س: صحيح = ملصق_ع ÷ 2؛
    عرف مركز_ص: صحيح = ملصق_ط ÷ 2؛

    عرف نافذة: مـلصق(ملصق_ع، ملصق_ط)؛
    ابدأ_رسم_النقش(نافذة، 1)؛

    // ارسم نافذة الكلام.
    عرف نقاط: مـصفوفة[صحيح]({
        مركز_س + ع ÷ 2 + 10، مركز_ص + ط ÷ 2 + 10،
        مركز_س + ع ÷ 2 + 10، مركز_ص - ط ÷ 2 - 10،
        مركز_س - ع ÷ 2 - 10، مركز_ص - ط ÷ 2 - 10،
        مركز_س - ع ÷ 2 - 10، مركز_ص + ط ÷ 2 + 10،
        مركز_س + ع ÷ 2 + 10، مركز_ص + ط ÷ 2 + 10،
    })؛
    ارسم_شكلا_مؤطرا(نقاط)؛
    اكتب_سطور(سطور، مركز_س + ع÷2، مركز_ص - ط÷2)؛

    أنه_رسم_النقش()؛

    // تأكد من عدم خروج الملصق من حدود الشاشة.
    إذا س + ملصق_ع ÷ 2 >= _عرض_النافذة_ س = _عرض_النافذة_ - ملصق_ع
    وإلا إذا س - ملصق_ع ÷ 2 < 0 س = 0
    وإلا س -= ملصق_ع ÷ 2؛

    إذا ص + ملصق_ط ÷ 2 >= _طول_النافذة_ س = _طول_النافذة_ - ملصق_ط
    وإلا إذا ص - ملصق_ط ÷ 2 < 0 ص = 0
    وإلا ص -= ملصق_ط ÷ 2؛

    نافذة.ضع(س، ص)؛

    أرجع نافذة؛
}


دالة ارسم_شكلا_مؤطرا (نقاط: مـصفوفة[صحيح]) {
    المرسم.حدد_نمط_الملء(
        لـون(255، 255، 255، 255).حول_لنص()،
        لـون(255، 255، 255، 255).حول_لنص()،
        0، 0، _عرض_النافذة_، _طول_النافذة_
    )؛
    المرسم.ارسم_مضلعا(نقاط.هات_الطول() ÷ 2، نقاط.صوان، 1)؛
    المرسم.حدد_نمط_القلم(لـون(50، 50، 50، 255).حول_لنص())؛
    المرسم.ارسم_مضلعا(نقاط.هات_الطول() ÷ 2، نقاط.صوان، 0)؛
}


دالة احسب_حجم_الكتابة (سطور: مـصفوفة[نـص]، عرض: سند[صحيح]، طول: سند[صحيح]) {
    عرض = طول = 0؛
    عرف م: صحيح؛
    لكل م = 0، م < سطور.هات_الطول()، ++م {
        عرف أبعاد: أبـعاد = المرسم.قس_أبعاد_كتابة(سطور(م)، _خط_فقاعات_الكلام_)؛
        طول += أبعاد.طول؛
        إذا أبعاد.عرض > عرض { عرض = أبعاد.عرض }؛
    }
}


دالة اكتب_سطور (سطور: مـصفوفة[نـص]، س: صحيح، ص: صحيح) {
    اكتب_سطور(سطور، س، ص، لـون(0، 0، 0، 255))؛
}


دالة اكتب_سطور (سطور: مـصفوفة[نـص]، س: صحيح، ص: صحيح، لون: سند[لـون]) {
    عين_لون_الرسم(لون.أحمر، لون.أخضر، لون.أزرق، لون.شفافية)؛
    عرف م: صحيح؛
    لكل م = 0، م < سطور.هات_الطول()، ++م {
        عرف أبعاد: أبـعاد = المرسم.قس_أبعاد_كتابة(سطور(م)، _خط_فقاعات_الكلام_)؛
        المرسم.ارسم_كتابة(سطور(م)، _خط_فقاعات_الكلام_، س، ص، 1)؛
        ص += أبعاد.طول؛
    }
}
