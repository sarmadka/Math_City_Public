دالة ابدأ_رسم_النقش (نقش: سند[نـقش_متعدد]، نظف: ثنائي، حالة: صحيح) {
    إذا نقش.نقوش.هات_الطول() == 0 {
        اكتب_في_الطرفية("خطأ: لم يُهيأ النقش بعد.")؛
        ارجع؛
    }
    مرسم_النقوش.غير_الأبعاد(نقش.عرض، نقش.طول)؛
    مرسم_النقوش.فرغ()؛
    إذا ليس نظف و نقش.نقوش(حالة).معرف != 0 {
        مرسم_النقوش.ارسم_صورة(نقش.نقوش(حالة)، 0، 0، نقش.عرض، نقش.طول، 1.0)؛
    }
    المرسم~عطل_التتبع = مرسم_النقوش؛
    النقش_الحالي~عطل_التتبع = نقش.نقوش(حالة)؛
}


دالة أنه_رسم_النقش {
    المرسم~عطل_التتبع = مرسم_العرض.رسم؛
    انتظر(النقش_الحالي.هيئ_من_مرسم(مرسم_النقوش))؛
}


دالة عين_لون_الرسم (لون: سند[لـون]) {
    عين_لون_الرسم(لون.أحمر، لون.أخضر، لون.أزرق، لون.شفافية)؛
}


دالة عين_لون_الرسم (ح: صـحيح، خ: صـحيح، ز: صـحيح، ش: صـحيح) {
    عرف ل: نـص = لـون(ح، خ، ز، ش).حول_لنص()؛
    المرسم.حدد_نمط_الملء(ل، ل، 0، 0، _عرض_النافذة_، _طول_النافذة_)؛
}


دالة ارسم_مستطيلا_مليئا (س1: صحيح، ص1: صحيح، س2: صحيح، ص2: صحيح) {
    المرسم.ارسم_مستطيلا_مليئا(س1، ص1، س2 - س1 + 1، ص2 - ص1 + 1)؛
}


دالة فرغ_مستطيلا (س1: صحيح، ص1: صحيح، س2: صحيح، ص2: صحيح) {
    المرسم.فرغ_مستطيلا(س1، ص1، س2 - س1 + 1، ص2 - ص1 + 1)؛
}


دالة حمل_صوتا(اسم_الملف: مـؤشر_محارف): سـندنا[مـؤجلات.مـؤجلة[سـندنا[صـوت]]] {
    عرف صوت: سـندنا[صـوت]؛
    صوت.أنشئ()؛
    أرجع صوت.هيئ(نـص(اسم_الملف)).ثم[سـندنا[صـوت]](مغلفة (صـحيح، م: سند[مـؤجلات.مـؤجلة[سـندنا[صـوت]]]) {
        م.قرر(صوت)؛
    })؛
}


دالة حدث {
    حدث(0)؛
}


عرف التطبيق_في_الخلفية: ثـنائي = 0؛

دالة حدث (انتظر: ثنائي) {
    بينما التطبيق_في_الخلفية {
        انتظر_حدثا()؛
        عالج_الأحداث()؛
    }

    ارسم_النافذة()؛

    إذا انتظر {
        // إن كان المستخدم يكبس على أحد أزرار القائمة العائمة فسنتمهل فقط بدل انتظار حدث من المستخدم
        // كي نتيح للقائمة العائمة تحديث الحالة عند انقضاء مهلة الكبسة.
        إذا القائمة_العائمة.الحالة != 0 نـظام.نم(100000)
        وإلا انتظر_حدثا()؛
    } وإلا نـظام.نم(1000)؛

    تحكم.صفر()؛
    عالج_الأحداث()؛
    حدث_النافذة()؛
}


دالة انتظر_مع_التحديث [صـنف_إرجاع: صنف = صـحيح] (م: سـندنا[مـؤجلات.مـؤجلة[صـنف_إرجاع]]): صـنف_إرجاع {
    إذا م.الحالة != مـؤجلات.حـالة._جديد_ أرجع م.النتيجة؛
    بينما 1 {
        حدث()؛
        إذا م.الحالة != مـؤجلات.حـالة._جديد_ اقطع؛
    }
    أرجع م.النتيجة؛
}


دالة حدث_النافذة {
    عرف ن: صحيح؛
    لكل ن = 0، ن < مـشهد.المشاهد.هات_الطول()، ++ن {
        مـشهد.المشاهد(ن).حدث()؛
    }
}


دالة ارسم_النافذة {
    // تتمة: هل نحتاج للتفريغ؟
    فرغ_مستطيلا(0، 0، _عرض_النافذة_، _طول_النافذة_)؛

    عرف ن: صحيح؛
    لكل ن = 0، ن < مـشهد.المشاهد.هات_الطول()، ++ن {
        مـشهد.المشاهد(ن).ارسم()؛
    }

    إذا إظهار_نافذة_إعادة_المحاولة نافذة_إعادة_المحاولة.ارسم(255)؛
}


دالة هات_الختم_الزمني_بالملي (): صحيح[64] {
    أرجع مـنصة_ويب.هات_الختم_الزمني()؛
}


دالة تمهل (ميلي_ثواني: صحيح[64]) {
    عرف البداية: صحيح[64] = هات_الختم_الزمني_بالملي()؛
    بينما 1 {
        إذا هات_الختم_الزمني_بالملي() - البداية >= ميلي_ثواني اقطع؛
        حدث()؛
        إذا القائمة_العائمة.التجاوز_مفعل أو القائمة_العائمة.العودة_مفعلة ارجع؛
    }
}

دالة هات_موضع_التشغيل_الرئيسي(): نـص {
    أرجع نـافذة.النموذج.المخزن_المحلي.هات_عنصرا("الموضع_الرئيسي")؛
}

دالة حدد_موضع_التشغيل_الرئيسي(م: نـص) {
    نـافذة.النموذج.المخزن_المحلي.حدد_عنصرا("الموضع_الرئيسي"، م)؛
}

دالة هات_موضع_التشغيل_الفرعي(): صـحيح {
    أرجع نـص.اقرأ_صحيح(نـافذة.النموذج.المخزن_المحلي.هات_عنصرا("الموضع_الفرعي"))؛
}

دالة حدد_موضع_التشغيل_الفرعي(م: صـحيح) {
    نـافذة.النموذج.المخزن_المحلي.حدد_عنصرا("الموضع_الفرعي"، نـص.املأ("%i"، م))؛
}

دالة هات_حالة_تشغيل_الموسيقى(): ثـنائي {
    عرف حالة: نـص = نـافذة.النموذج.المخزن_المحلي.هات_عنصرا("حالة_تشغيل_الموسيقى")؛
    إذا حالة == "" أرجع 1
    وإلا أرجع نـص.اقرأ_صحيح(حالة) == 1؛
}

دالة حدد_حالة_تشغيل_الموسيقى(م: ثـنائي) {
    نـافذة.النموذج.المخزن_المحلي.حدد_عنصرا("حالة_تشغيل_الموسيقى"، نـص.املأ("%i"، م~مثل[صـحيح]))؛
}
