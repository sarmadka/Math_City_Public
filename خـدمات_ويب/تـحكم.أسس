صنف تـحكم {
    عرف أعلى: ثنائي = 0؛
    عرف أعلى_للتو: ثنائي = 0؛

    عرف يمين: ثنائي = 0؛
    عرف يمين_للتو: ثنائي = 0؛

    عرف يسار: ثنائي = 0؛
    عرف يسار_للتو: ثنائي = 0؛

    عرف أسفل: ثنائي = 0؛
    عرف أسفل_للتو: ثنائي = 0؛

    عرف تفاعل: ثنائي = 0؛
    عرف تفاعل_للتو: ثنائي = 0؛

    عرف محو: ثنائي = 0؛
    عرف محو_للتو: ثنائي = 0؛

    عرف الرقم: صحيح = -1؛

    عرف نقرة: ثـنائي = 0؛
    عرف نقرة_للتو: ثـنائي = 0؛

    عرف س: صـحيح = 0؛
    عرف ص: صـحيح = 0؛

    عملية هذا.تفاعل_أو_نقرة_للتو: ثـنائي {
        أرجع هذا.تفاعل_للتو أو هذا.نقرة_للتو؛
    }

    عرف عند_النقر: مغلفة ()؛

    عملية هذا.صفر() {
        هذا.أعلى_للتو = 0؛
        هذا.يمين_للتو = 0؛
        هذا.يسار_للتو = 0؛
        هذا.أسفل_للتو = 0؛
        هذا.تفاعل_للتو = 0؛
        هذا.محو_للتو = 0؛
        هذا.نقرة_للتو = 0؛
        هذا.الرقم = -1؛
    }

    عملية هذا.عند_بدء_الكبسة (رمز_المفتاح: نـص) {
        إذا رمز_المفتاح == "ArrowUp" هذا.أعلى = هذا.أعلى_للتو = 1
        وإلا إذا رمز_المفتاح == "ArrowDown" هذا.أسفل = هذا.أسفل_للتو = 1
        وإلا إذا رمز_المفتاح == "ArrowRight" هذا.يمين = هذا.يمين_للتو = 1
        وإلا إذا رمز_المفتاح == "ArrowLeft" هذا.يسار = هذا.يسار_للتو = 1
        وإلا إذا رمز_المفتاح == "Space" هذا.تفاعل = هذا.تفاعل_للتو = 1
        وإلا إذا رمز_المفتاح == "Backspace" هذا.محو = هذا.محو_للتو = 1
        وإلا إذا نـص.قارن(رمز_المفتاح، "Digit"، 5) == 0 {
            هذا.الرقم = رمز_المفتاح(5) - '0'؛
        }
    }

    عملية هذا.عند_انتهاء_الكبسة (رمز_المفتاح: نـص) {
        إذا رمز_المفتاح == "ArrowUp" هذا.أعلى = 0
        وإلا إذا رمز_المفتاح == "ArrowDown" هذا.أسفل = 0
        وإلا إذا رمز_المفتاح == "ArrowRight" هذا.يمين = 0
        وإلا إذا رمز_المفتاح == "ArrowLeft" هذا.يسار = 0
        وإلا إذا رمز_المفتاح == "Space" هذا.تفاعل = 0
        وإلا إذا رمز_المفتاح == "Backspace" هذا.محو = 0
    }
}
