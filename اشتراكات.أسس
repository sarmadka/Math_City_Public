@منفذ_بياني["GET", "/api/subscription"]
دالة ابدأ_الاشتراك(اتصال:مؤشر[بـننف.اتـصال]) {
    عرف العدد: صـحيح = 0؛
    عرف المعطى: مـؤشر_محارف = بـننف.هات_معلومات_الطلب(اتصال)~محتوى.نص_الاستعلام؛
    إذا المعطى != 0 {
        عرف رمز_الولوج: نـص = شـبكة.فك_عنوانيا(المعطى)؛
        عرف اسم_المزود: نـص؛
        عرف رمز_الولوج_الخارجي: نـص؛
        عرف معرف_المستخدم: نـص؛
        طـرفية.اطبع("======== رمز الولوج: %s\ج"، رمز_الولوج.صوان)؛
        إذا ليس هـوية.تحقق_من_رمز_الولوج(رمز_الولوج، اسم_المزود، رمز_الولوج_الخارجي، معرف_المستخدم) {
            طـرفية.اطبع("======== فشل التحقق من رمز الولوج\ج")؛
            أرجع_ردا(401، ""، اتصال)؛
        } وإلا {
            طـرفية.اطبع("======== نجح التحقق من رمز الولوج. معرف المستخدم: %s\ج"، معرف_المستخدم.صوان)؛
            عرف مستخدم: سـندنا[مـستخدم] = هات_المستخدم(معرف_المستخدم)؛
            عرف جلسة_الدفع: سـندنا[جـلسة_دفع] = هات_جلسة_الدفع_النشطة(معرف_المستخدم)؛
            إذا مستخدم.أهو_عدم() {
                طـرفية.اطبع("======== لم يُعثر على المستخدم \ج")؛
                أرجع_ردا(401، ""، اتصال)؛
            } وإلا إذا مستخدم.مشترك {
                أرجع_ردا(200، "1"، اتصال)؛
            } وإلا إذا ليس جلسة_الدفع.أهو_عدم() {
                عرف رد: نـص = أتمم_جلسة_الدفع(مستخدم، جلسة_الدفع)؛
                إذا رد != "" {
                    أرجع_ردا(200، رد، اتصال)؛
                } وإلا {
                    أرجع_ردا(422، ""، اتصال)؛
                }
            } وإلا {
                عرف رابط: نـص؛
                ابدأ_جلسة_الدفع(مستخدم).{
                    إذا ليس هذا أرجع_ردا(500، "Unexpected sever error"، اتصال)؛
                    رابط = هذا.القيمة؛
                }؛
                أرجع_ردا(200، رابط، اتصال)؛
            }
        }
    } وإلا {
        طـرفية.اطبع("subscription: خطأ في بيانات الطلب.\ج")؛
        أرجع_ردا(500، ""، اتصال)؛
    }
}


دالة ابدأ_جلسة_الدفع(مستخدم: سـندنا[مـستخدم]): لـا_مضمون[نـص] {
    عرف وكيل: سـترايب.وكـيل(إعـدادات.مفتاح_سترايب)؛
    عرف جلسة_الدفع: سـندنا[جـلسة_دفع]؛
    وكيل.أنشئ_جلسة_إتمام_شراء(
        تـطبيق[نـص، صـحيح]().حدد(إعـدادات.معرف_منتج_سترايب، 1)،
        هات_موقع_إكمال_الاشتراك()
    ).{
        إذا ليس هذا أرجع لـا_مضمون[نـص].فشل(هذا.الخطأ)؛
        جلسة_الدفع = أنشئ_جلسة_دفع(مستخدم.المعرف، هذا.القيمة)؛
    }؛
    عرف رابط: نـص؛
    وكيل.هات_جلسة_إتمام_الشراء(جلسة_الدفع.المعرف).{
        إذا ليس هذا أرجع لـا_مضمون[نـص].فشل(هذا.الخطأ)؛
        رابط = هذا.الرابط؛
    }؛
    أرجع لـا_مضمون[نـص].نجاح(رابط)؛
}


دالة أتمم_جلسة_الدفع(مستخدم: سـندنا[مـستخدم]، جلسة_الدفع: سـندنا[جـلسة_دفع]): نـص {
    عرف وكيل: سـترايب.وكـيل(إعـدادات.مفتاح_سترايب)؛
    وكيل.هات_جلسة_إتمام_الشراء(جلسة_الدفع.المعرف).{
        إذا هذا {
            إذا هذا.حالة_الدفع == "paid" {
                جلسة_الدفع.الحالة = جـلسة_دفع.حـالة._مكتمل_؛
                جلسة_الدفع.تاريخ_التحديث = هات_التاريخ_الحالي()؛
                جلسة_الدفع.احفظ()؛
                مستخدم.مشترك = 1؛
                مستخدم.تاريخ_التحديث = هات_التاريخ_الحالي()؛
                مستخدم.احفظ()؛
                أرجع نـص("1")؛
            } وإلا {
                أرجع هذا.الرابط؛
            }
        }
    }
    أرجع نـص()؛
}


دالة أرجع_ردا(رمز: صـحيح، متن: مـؤشر_محارف، اتصال: مؤشر[بـننف.اتـصال]) {
    بـننف.اطبع(اتصال, "HTTP/1.1 %d error\r\n"، رمز)؛
    بـننف.اطبع(اتصال, "Content-Type: text/plain\r\n")؛
    بـننف.اطبع(اتصال, "Cache-Control: no-cache\r\n")؛
    بـننف.اطبع(اتصال, "Content-Length: %d\r\n\r\n", نـص.هات_الطول(متن))؛
    بـننف.اطبع(اتصال, "%s"، متن)؛
}


دالة هات_موقع_إكمال_الاشتراك(): نـص {
    أرجع إعـدادات.عنوان_الخادم + "/subscribed"؛
}

دالة تحقق_من_الإشتراك {
    عرف داخل: ثـنائي = انتظر[ثـنائي](هـوية.تحقق_من_حالة_المصادقة())؛
    إذا ليس داخل {
        أظهر_صفحة_تسجيل_الدخول()؛
    }

    عرف رمز_الولوج: نـص = هـوية.هات_رمز_الولوج()؛
    عرف المسار: نـص = نـص.املأ("/api/subscription?%s"، شـبكة.شفر_عنوانيا(رمز_الولوج).صوان)؛
    عرف جيسون: جـيسون = انتظر[جـيسون](أرسل_نداء("GET"، المسار، 0، 0، 10000))؛
    إذا جيسون("eventData")("status")~مثل[صـحيح] == 200 {
        عرف المتن: نـص = جيسون("eventData")("body")؛
        إذا المتن != "1" {
            أعد_التوجيه(المتن)؛
            نفذ_حلقة_معالجة_الأحداث()؛
        }
    } وإلا إذا جيسون("eventData")("status")~مثل[صـحيح] == 0 {
        أظهر_إشعارا(نـص("حصل خطأ غير متوقع. يُرجى التحقق من اتصال الشبكة.")، نـص("عد للبداية"))؛
        أعد_التوجيه("/")؛
        نفذ_حلقة_معالجة_الأحداث()؛
    } وإلا {
        أظهر_إشعارا(نـص("حصل خطأ غير متوقع. تواصل معنا لمحاولة حل المشكلة.")، نـص("اتصل بنا"))؛
        أعد_التوجيه("/contact")؛
        نفذ_حلقة_معالجة_الأحداث()؛
    }
}

دالة تحقق_من_اكتمال_الاشتراك {
    عرف داخل: ثـنائي = انتظر[ثـنائي](هـوية.تحقق_من_حالة_المصادقة())؛
    إذا ليس داخل {
        أظهر_إشعارا(نـص("حصل خطأ غير متوقع.")، نـص("عد للبداية"))؛
        أعد_التوجيه("/")؛
        نفذ_حلقة_معالجة_الأحداث()؛
    }

    عرف رمز_الولوج: نـص = هـوية.هات_رمز_الولوج()؛
    عرف المسار: نـص = نـص.املأ("/api/subscription?%s"، شـبكة.شفر_عنوانيا(رمز_الولوج).صوان)؛
    عرف جيسون: جـيسون = انتظر[جـيسون](أرسل_نداء("GET"، المسار، 0، 0، 10000))؛
    إذا جيسون("eventData")("status")~مثل[صـحيح] == 200 {
        عرف المتن: نـص = جيسون("eventData")("body")؛
        إذا المتن != "1" {
            أظهر_إشعارا(نـص("حصل خطأ غير متوقع. تواصل معنا لمحاولة حل المشكلة.")، نـص("اتصل بنا"))؛
            أعد_التوجيه("/contact")؛
            نفذ_حلقة_معالجة_الأحداث()؛
        }
    } وإلا إذا جيسون("eventData")("status")~مثل[صـحيح] == 0 {
        أظهر_إشعارا(نـص("حصل خطأ غير متوقع. يُرجى التحقق من اتصال الشبكة.")، نـص("عد للبداية"))؛
        أعد_التوجيه("/")؛
        نفذ_حلقة_معالجة_الأحداث()؛
    } وإلا {
        أظهر_إشعارا(نـص("حصل خطأ غير متوقع. تواصل معنا لمحاولة حل المشكلة.")، نـص("اتصل بنا"))؛
        أعد_التوجيه("/contact")؛
        نفذ_حلقة_معالجة_الأحداث()؛
    }
}
