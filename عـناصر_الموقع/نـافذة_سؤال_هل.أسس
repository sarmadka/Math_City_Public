صنف نـافذة_سؤال_هل {
    @حقنة عرف مركب: مـركب؛
    // المتغيرات
    عرف عند_الإجابة: مغلفة (جواب: ثـنائي)؛

    عملية هذا~هيئ(ن: نـص) {
        عرف الكائن: سند[هذا_الصنف](هذا)؛
        هذا.المشهد = صـندوق().{
            الطراز.{
                الهامش = مـسافة4.نقاط(10)؛
                الحشوة = مـسافة4.نقاط(10)؛
                الخلفية = خـلفية(لـون("fff"))؛
                نصف_قطر_الإطار = مـسافة4.نقاط(5)؛
                ظل_الصندوق = ظـل_صندوق(مـسافة.نقاط(1)، مـسافة.نقاط(1)، مـسافة.نقاط(3)، مـسافة.نقاط(3)، لـون("0008"))؛
                الإظهار = إظـهار._كتلة_ضمنية_؛
                فئة_الخط = "SayfFont"؛
                حجم_الخط = مـسافة.نقاط(20)؛
                الاتجاه = اتـجاه._من_اليمين_؛
            }؛
            أضف_فروع({
                كـتابة().{
                    النص = ن؛
                }،
                صـندوق().{
                    الطراز.{
                        الإظهار = إظـهار._مرن_؛
                        النسق = نـسق._سطر_؛
                        المحاذاة = مـحاذاة._وسط_؛
                        ملء_السطر = مـلء_سطر._مسافة_محيطية_؛
                    }؛
                    أضف_فروع({
                        ارتـباط_تشعبي().{
                            الطراز.زخرفة_النص = زخـرفة_نص._بلا_؛
                            العنوان = نـص("#")؛
                            حدد_الفرع(كـتابة().{
                                النص = نـص("نعم")؛
                                الطراز.{
                                    الهامش = مـسافة4.نقاط(20)؛
                                    لون_الخط = لـون(0، 100، 200)؛
                                }؛
                            })؛
                            عند_الضغط.المبدئي_مكبوح = 1؛
                            عند_الضغط.اربط(مغلفة (سند[ودجـة]، سند[صـحيح]) {
                                إذا ليس الكائن.عند_الإجابة.أهو_عدم() الكائن.عند_الإجابة(1)؛
                            })؛
                        }،
                        ارتـباط_تشعبي().{
                            الطراز.زخرفة_النص = زخـرفة_نص._بلا_؛
                            العنوان = نـص("#")؛
                            حدد_الفرع(كـتابة().{
                                النص = نـص("لا")؛
                                الطراز.{
                                    الهامش = مـسافة4.نقاط(20)؛
                                    لون_الخط = لـون(0، 100، 200)؛
                                }؛
                            })؛
                            عند_الضغط.المبدئي_مكبوح = 1؛
                            عند_الضغط.اربط(مغلفة (سند[ودجـة]، سند[صـحيح]) {
                                إذا ليس الكائن.عند_الإجابة.أهو_عدم() الكائن.عند_الإجابة(0)؛
                            })؛
                        }،
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف (ن: نـص): سـندنا[نـافذة_سؤال_هل] {
        أرجع سـندنا[نـافذة_سؤال_هل]().{ احجز()~هيئ(ن) }؛
    }
}
