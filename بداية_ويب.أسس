اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـظام"؛
اشمل "مـتم/نـص"؛
استخدم مـتم؛

//==============================================================================
// معالجة المعطيات
عرف بناء_نسخة_تنفيذية: ثـنائي؛
إذا الـعملية.عدد_المعطيات == 3 {
    عرف المعطى: مـؤشر_محارف = الـعملية.المعطيات~محتوى(الـعملية.عدد_المعطيات - 1)؛
    إذا نـص.أمتطابق(المعطى، "انشر") {
        بناء_نسخة_تنفيذية = 1؛
    } وإلا {
        طـرفية.اطبع(
            "خطأ: معطى غير صالح\ج"
            "الاستخدام:\ج"
            "  الأسس بداية_ويب.أسس [انشر]\ج"
        )؛
        نـظام.اخرج(0)؛
    }
} وإلا {
    بناء_نسخة_تنفيذية = 0؛
}

//==============================================================================
// الاعتماديات

طـرفية.اطبع("شمول المكتبات وملفات المشروع...\ج")؛

اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/ريـاضيات"؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/نـم"؛
اشمل "مـتم/أخطاء"؛
اشمل "مـتم/لـا_مضمون"؛
اشمل "مـتم/بـعدم"؛
اشمل "مـتم/شـبكة"؛
اشمل "مـتم/نـمط"؛
اشمل "مـتم/وقـت"؛
اشمل "مغلفة"؛
اشمل "الـقلب/أسـاسيات"؛
اشمل "الـقلب/بـيانات"؛
اشمل "نـبم"؛
اشمل "بـناء"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
مـحا.اشمل_ملف("Alusus/Promises"، "مـؤجلات.أسس")؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Identity"، "هـوية.أسس")؛
مـحا.اشمل_ملف("Alusus/Rows"، { "صـفوف.أسس"، "مـشغلات/بـوستغرس.أسس" })؛
مـحا.اشمل_ملف("Alusus/Stripe"، "سـترايب.أسس")؛
مـحا.اشمل_ملف("Alusus/Recaptcha"، "ريـكابجا.أسس")؛
مـحا.اشمل_ملف("Alusus/MarkdownTranslator"، "مـترجم_ماركداون.أسس")؛

استخدم مـنصة_ويب؛

اشمل "مساعدات"؛
اشمل "إعـدادات"؛
اشمل "قاعدة_البيانات"؛
اشمل "اشتراكات"؛
اشمل "خـدمات_ويب/رئيسية"؛
اشمل "خـدمات_ويب/نـقش_متعدد"؛
اشمل "خـدمات_ويب/كتابة"؛
اشمل "خـدمات_ويب/صـوت"؛
اشمل "خـدمات_ويب/تـحكم"؛
اشمل "عـناصر_الموقع/أظهر_الصفحة_الرئيسية"؛
اشمل "عـناصر_الموقع/الـصفحة_الرئيسية"؛
اشمل "عـناصر_الموقع/أظهر_صفحة_تسجيل_الدخول"؛
اشمل "عـناصر_الموقع/أظهر_إشعارا"؛
اشمل "عـناصر_الموقع/أظهر_اللعبة"؛
اشمل "عـناصر_الموقع/مـشهد_الاتصال"؛
اشمل "عـناصر_الموقع/مـشهد_سياسة_الخصوصية"؛
اشمل "عـناصر_الموقع/أرسل_بريدًا_إلكترونيا"؛
اشمل "عـناصر_الموقع/نـافذة_سؤال_هل"؛
اشمل "عـناصر_الموقع/قـائمة_اللغة"؛

اشمل "سيف"؛

//=============================================================================
// تعريفات عمومية

عرف المكداس: سـندنا[مـكداس]؛
عرف المشهد_الرئيسي: سـندنا[صـندوق]؛
عرف مرسم_العرض: سـندنا[مـرسم]؛
عرف مرسم_النقوش: سـندنا[مـورد_مرسم]؛
عرف المرسم: سند[رسـم]؛
عرف النقش_الحالي: سند[مـورد_صورة]؛
عرف _خط_فقاعات_الكلام_: "45px SayfFont"؛
عرف _لون_الخلفية_: "000"؛
عرف تجهيز_الكابجا: سـندنا[مـؤجلات.مـؤجلة[صـحيح]]؛
عرف تحميل_الموارد: سـندنا[مـؤجلات.مـؤجلة[صـحيح]]؛
عرف صوت_فارغ: صـوت؛

//=============================================================================
// أصناف

صنف ربـاعية {
    عرف أعلى: عائم = 0؛
    عرف يمين: عائم = 0؛
    عرف أسفل: عائم = 0؛
    عرف يسار: عائم = 0؛
}

//=============================================================================
// الخادم

@مسار_موارد عرف مسار_الصور: "رسوم/"؛
@مسار_موارد عرف مسار_الأصوات: "أصوات/"؛
@مسار_موارد عرف مسار_الخطوط: "خطوط/"؛

//=============================================================================
// الصفحة

@منفذ_مرئي["/*"]
@عنوان["سيف في مدينة الرياضيات"]
@تطبيق_ويب["موارد_أخرى/manifest.json"، "إصدار8"]
@أيقونة["رسوم/icon.png"]
@خزن_مسبق["/"، "/login"، "/رسوم/شاشة_البداية.png"، "/أصوات/INTRO.mp3"، "/خطوط/Jawadtaut-1.otf"]
@خزن_تفاعلي["^/رسوم/"، "^/أصوات/"، "^/api/getframecount"]
دالة رئيسي {
    تحميل_الموارد = مـؤجلات.مـؤجلة[صـحيح].الكل({
        حمل_خط("SayfFont", "/خطوط/Jawadtaut-1.otf")،
        صوت_فارغ.هيئ(نـص("/أصوات/فارغ.wav"))،
    }).تجاهل_النتيجة()؛
    هيئ_النافذة()؛

    إذا نـافذة.النموذج.مسار_الموقع == "/login" {
        // عودة للموقع لإكمال تسجيل الدخول.

        إذا انتظر[ثـنائي](هـوية.أكمل_المصادقة(هات_موقع_إكمال_تسجيل_الدخول())) {
            نـافذة.النموذج.ادفع_مسارا("/")؛
            أظهر_إشعارا(نـص("اكتمل تسجيل الدخول بنجاح")، نـص("استمر"))؛
            تحقق_من_الإشتراك()؛
            انتظر(تحميل_الموارد)؛
            أظهر_اللعبة(1)؛
        } وإلا {
            أظهر_إشعارا(نـص("حصل خطأ غير متوقع في تسجيل الدخول")، نـص("اتصل بنا"))؛
            أعد_التوجيه("/contact")؛
        }

    } وإلا إذا نـافذة.النموذج.مسار_الموقع == "/subscribed" {
        // عودة للموقع بعد اكتمال الدفع.

        تحقق_من_اكتمال_الاشتراك()؛
        نـافذة.النموذج.ادفع_مسارا("/")؛
        أظهر_إشعارا(نـص("اكتمل الاشتراك بنجاح")، نـص("استمر"))؛
        انتظر(تحميل_الموارد)؛
        أظهر_اللعبة(1)؛

    } وإلا إذا نـافذة.النموذج.مسار_الموقع == "/contact"
      أو نـافذة.النموذج.مسار_الموقع == "/privacy-policy/en"
      أو نـافذة.النموذج.مسار_الموقع == "/privacy-policy/ar" {
        // زيارة صفحة الاتصال أو سياسة الخصوصية

        أظهر_الصفحة_الرئيسية()؛

        // نحتاج لتشغيل اللعبة في حال خرج المستخدم من صفحة الاتصال وقرر تشغيل اللعبة.
        انتظر(تحميل_الموارد)؛
        أظهر_اللعبة(0)؛

    } وإلا إذا نـافذة.النموذج.مسار_الموقع == "/" {
        // زيارة جديدة للموقع.

        إذا نـص.أمتطابق(هات_نمط_عرض_التطبيق()~مثل[مـؤشر_محارف]، "browser") {
            أظهر_الصفحة_الرئيسية()؛
        }

        انتظر(تحميل_الموارد)؛
        أظهر_اللعبة(0)؛

    } وإلا {
        // زيارة لعنوان غير صالح.

        أظهر_إشعارا(نـص("عنوان غير صحيح")، نـص("عد للبداية"))؛
        أعد_التوجيه("/")؛
    }
}

دالة هيئ_النافذة {
    نـافذة.النموذج.الطراز.{
        العرض = مـسافة.مئوي(100)؛
        الطول = مـسافة.مئوي(100)؛
        الهامش = مـسافة4.نقاط(0)؛
        الحشوة = مـسافة4.نقاط(0)؛
    }؛
    نـافذة.النموذج.حدد_المشهد(مـكداس().{
        المكداس = هذا؛
        الطراز.{
            العرض = مـسافة.مئوي(100)؛
            الطول = مـسافة.مئوي(100)؛
        }؛
        حدد_الانتقال(نـص("دخول")، أنشئ_انتقال_انزلاق_مكداس(0.7، 1، 0، 1))؛
        حدد_الانتقال(نـص("خروج")، أنشئ_انتقال_انزلاق_مكداس(1، 1، 1، 1))؛
        أضف(صـندوق().{
            المشهد_الرئيسي = هذا؛
        })؛
    })؛
    نـافذة.النموذج.عند_استلام_رسالة.اربط(مغلفة (نافذة: سند[نـافذة]، حمولة: سند[جـيسون]) {
        إذا حمولة("type")~مثل[نـص] == "app_update_available" {
            أظهر_إشعار_التحديث()؛
        }
    })؛
    إذا أيوجد_تحديث_للتطبيق() {
        أظهر_إشعار_التحديث()؛
    }
}

دالة أظهر_إشعار_التحديث {
    المكداس.أضف(
        صـندوق().{
            الطراز.{
                الطول = مـسافة.مئوي(100)؛
                العرض = مـسافة.مئوي(100)؛
                المحاذاة = مـحاذاة._وسط_؛
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._عمود_؛
            }؛
            أضف_فروع({
                نـافذة_سؤال_هل(نـص("يتوفر تحديث. هل ترغب بتنزيله؟")).{
                    عند_الإجابة = مغلفة (إجابة: ثـنائي) {
                        إذا إجابة {
                            حدث_التطبيق()؛
                        } وإلا {
                            المكداس.أزل(نـص("خروج"))؛
                        }
                    }
                }
            })؛
        }،
        نـص("دخول")
        )؛
}

دالة هات_موقع_إكمال_تسجيل_الدخول(): نـص {
    أرجع نـافذة.النموذج.بروتوكول_الموقع + "//" + نـافذة.النموذج.خادم_الموقع + "/login"؛
}

//==============================================================================
// إدارة المشروع

دالة ابدأ_خادم_سيف {
    هـوية.هيئ_واجهة_رست("rsa_key"، مغلفة (جواب: هـوية.جـواب_مصادقة): نـص {
        أرجع هات_المستخدم(جواب)؛
    })؛
    هـوية.أضف_مزودا(هـوية.مـزود_جوجل(إعـدادات.معرف_تطبيق_جوجل، إعـدادات.كلمة_سر_جوجل))؛
    ريـكابجا.هيئ_الخادم()؛
    هيئ_قاعدة_البيانات()؛
    طـرفية.اطبع("تشغيل الخادم على المنفذ 8010...\nURL: http://localhost:8010/\n")؛
    ابدأ_الخادم({ "listening_ports"، "8010"، "static_file_max_age"، "0" })؛
    طـرفية.اطبع("الخادم جاهز\ج")؛
    بينما 1 نـظام.نم(100000)؛
}

@تصدير[main]
دالة مدخل (عدد_المعطيات: صـحيح، المعطيات: مؤشر[مصفوفة[مـؤشر_محارف]]): صـحيح {
    مسار_الموارد_الرئيسية = "Assets/"؛
    مسار_المنافذ_المرئية = "Wasm/"؛
    أضف_مسار_موارد(نـص("/")، نـص("موارد_أخرى/"))؛

    ابدأ_خادم_سيف()؛
    أرجع 0؛
}

دالة ابن {
    طـرفية.اطبع("بناء النسخة التنفيذية...\ج")؛
    نـظام.شغل("mkdir -p Build")؛
    نـظام.شغل("cp -r رسوم Build/")؛
    نـظام.شغل("cp -r أصوات Build/")؛
    نـظام.شغل("cp -r خطوط Build/")؛
    نـظام.شغل("cp -r موارد_أخرى Build/")؛
    نـظام.شغل("cp rsa_key* Build/")؛
    عرف تنفيذي: بـناء.تـنفيذي(مدخل~شبم، "Build/sayf")؛
    تنفيذي.أضف_اعتماديات(مـنصة_ويب.هات_اعتماديات_البناء())؛
    تنفيذي.أضف_اعتماديات(شـبكة.هات_اعتماديات_البناء())؛
    تنفيذي.أضف_اعتماديات(صـفوف.مـشغل_بوستغرس.هات_اعتماديات_البناء())؛
    إذا تنفيذي.أنتج() {
        مـتم.طـرفية.اطبع("اكتمل البناء.\ج")؛
    } وإلا {
        نـظام.فشل(1، "فشل البناء")؛
    }؛
}

دالة انشر {
    // تفحص الإعدادات.
    إذا إعـدادات.منفذ_النشر == "" أو
        إعـدادات.اسم_المستخدم_للنشر == "" أو
        إعـدادات.عنوان_الخادم_للنشر == "" أو
        إعـدادات.مسار_النشر == "" {
            نـظام.فشل(1، "إعدادات النشر غير مضبوطة. يرجى ضبط المتغيرات المحيطية التالية:\ج"
                ‏"SAYF_PUBLISH_USERNAME\ج"
                ‏"SAYF_PUBLISH_HOST\ج"
                ‏"SAYF_PUBLISH_PORT\ج"
                ‏"SAYF_PUBLISH_PATH\ج"
                "بالإضافة للمتغيرات المحيطية يجب ضبط مفتاح SSH في الخادم\ج"
            )؛
        }
    // تحميل البرنامج للخادم.
    عرف أمر: نـص = نـص.املأ("rsync -avhr -e 'ssh -p %s' ./Build/* %s@%s:%s"،
        إعـدادات.منفذ_النشر.صوان،
        إعـدادات.اسم_المستخدم_للنشر.صوان،
        إعـدادات.عنوان_الخادم_للنشر.صوان،
        إعـدادات.مسار_النشر.صوان
    )؛
    إذا نـظام.شغل(أمر) != 0 {
        نـظام.فشل(1، "فشل النشر: فشل تحميل البرنامج للخادم.")؛
    }
    طـرفية.اطبع("حُمل البرنامج للخادم.\ج")؛
    // إعادة تشغيل الخادم.
    أمر = نـص.املأ("ssh -p %s %s@%s 'systemctl restart sayf'"،
        إعـدادات.منفذ_النشر.صوان،
        إعـدادات.اسم_المستخدم_للنشر.صوان،
        إعـدادات.عنوان_الخادم_للنشر.صوان
    )؛
    إذا نـظام.شغل(أمر) != 0 {
        نـظام.فشل(1، "فشل النشر: فشلت إعادة تشغيل الخادم.")؛
    }
    طـرفية.اطبع("أعيد تشغيل الخادم.\ج")؛
    طـرفية.اطبع("تم النشر بنجاح.\ج")؛
}

مسار_الموارد_الرئيسية = "Build/Assets/"؛
مسار_المنافذ_المرئية = "Build/Wasm/"؛

إذا بناء_نسخة_تنفيذية {
    ابن()؛
    انشر()؛
    نـظام.اخرج(0)؛
}

// تنفيذ مباشر
طـرفية.اطبع("تجهيز الخادم...\ج")؛
أضف_مسار_موارد(نـص("/")، نـص("Build/موارد_أخرى/"))؛
ابدأ_خادم_سيف()؛
