ماكرو سلسلة_محارف_من_متغير[المتغير] {
    تمهيد {
        نـبم.مدير_شبم.احشر_شبم(الـقلب.بـيانات.شـبم.نـص_حرفي(المتغير))؛
    }
}

ماكرو سلسلة_محارف_من_متغير_محيطي[اسم_المتغير] {
    تمهيد {
        عرف نص: نـص = نـظام.هات_متغير_محيطي(اسم_المتغير)؛
        نـبم.مدير_شبم.احشر_شبم(الـقلب.بـيانات.شـبم.نـص_حرفي(نص))؛
    }
}

ماكرو نص_لتنف_من_ملف_ماركداون[اسم_الملف] {
    تمهيد {
        استخدم الـقلب.أسـاسيات؛
        استخدم الـقلب.بـيانات.شـبم؛
        عرف نص: نـص = نـم.اقرأ_ملف(اسم_الملف)؛
        عرف مترجم: مـترجم_ماركداون[نـمط.مـطابق]؛
        مترجم.ألسنة_جديدة_للروابط = 1؛
        نـبم.مدير_شبم.احشر_شبم(
            (شبم نـص(النص))،
            مـتم.تـطبيق[نـص، سند[كـائن_بهوية]]().حدد(نـص("النص")، نـص_حرفي(مترجم.ترجم(نص)))
        )؛
    }
}

ماكرو محارف_أو_قيمة_مبدئية[القيمة، القيمة_المبدئية] مـؤشر_محارف().{
    هذا = القيمة؛
    إذا هذا == 0 هذا = القيمة_المبدئية؛
}؛

دالة رمّز_إكسمل(نص: نـص): نـص {
    أرجع نص.استبدل("\\"، "\\\\").استبدل("\ج"، "\\n").استبدل("\""، "\\\"")؛
}

دالة هات_التاريخ_الحالي(): نـص {
    عرف ختم_زمني: صـحيح_متكيف = وقـت.هات_الختم_الزمني(0)؛
    عرف وقت: وقـت.وقـت_مفصل؛
    وقـت.هات_وقـت_مفصل(ختم_زمني~مؤشر، وقت~مؤشر)؛
    عرف صوان: مصفوفة[مـحرف، 100]؛
    نـص.عين(
        صوان~مؤشر، "%04d-%02d-%02d %02d:%02d:%02d"،
        وقت.سنة + 1900، وقت.شهر + 1، وقت.يوم، وقت.ساعة، وقت.دقيقة، وقت.ثانية
    )؛
    أرجع نـص(صوان~مؤشر)؛
}
