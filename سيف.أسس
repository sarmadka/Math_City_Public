اشمل "خدمات/رئيسية.أسس"؛
اشمل "خدمات/مـشهد.أسس"؛
اشمل "خدمات/مـلصق.أسس"؛
اشمل "خدمات/كـلام.أسس"؛
اشمل "خدمات/مـحاولات.أسس"؛
اشمل "خدمات/لـوح_مفاتيح.أسس"؛
اشمل "خدمات/قـائمة_عائمة.أسس"؛
اشمل "خدمات/دخان"؛

استخدم مـتم؛

اشمل "مشاهد/الحلم1_جزء1.أسس"؛
// اشمل "مشاهد/الحلم1_جزء2.أسس"؛
// اشمل "مشاهد/الحلم1_جزء3.أسس"؛
اشمل "مشاهد/مشهد1.أسس"؛

// اشمل "مشاهد/الحلم2_جزء1.أسس"؛
// اشمل "مشاهد/الحلم2_جزء2.أسس"؛
// اشمل "مشاهد/الحلم2_جزء3.أسس"؛
// اشمل "مشاهد/مشهد2.أسس"؛

// اشمل "مشاهد/الحلم3_جزء1.أسس"؛
// اشمل "مشاهد/الحلم3_جزء2.أسس"؛
// اشمل "مشاهد/الحلم3_جزء3.أسس"؛
// اشمل "مشاهد/مشهد3.أسس"؛

اشمل "مشاهد/القصة"؛

//============================
// المتغيرات والثوابت العمومية

عرف تحكم: تـحكم؛
عرف القائمة_العائمة: سند[قـائمة_عائمة]؛
عرف _عرض_النافذة_: 1280؛
عرف _طول_النافذة_: 800؛

//===========
// الماكروهات

ماكرو تحقق_من_العودة {
    إذا القائمة_العائمة.العودة_مفعلة {
        صـوت.ألبث_جميع_الأصوات()؛
        أخف_رويدا(مشهد، 1000)؛
        ارجع 0؛
    }
}

ماكرو تحقق_من_التجاوز_والعودة {
    إذا القائمة_العائمة.العودة_مفعلة {
        صـوت.ألبث_جميع_الأصوات()؛
        أخف_رويدا(مشهد، 1000)؛
        ارجع 0؛
    } وإلا إذا القائمة_العائمة.التجاوز_مفعل {
        صـوت.ألبث_جميع_الأصوات()؛
        أخف_رويدا(مشهد، 1000)؛
        ارجع 1؛
    }
}

ماكرو تمهل_مع_تمكين_التجاوز_والعودة[مدة] {
    تمهل(مدة)؛
    تحقق_من_التجاوز_والعودة[]؛
}

ماكرو تمهل_مع_تمكين_العودة[مدة] {
    تمهل(مدة)؛
    تحقق_من_العودة[]؛
}

//=======
// الأصناف

صنف حـلم {
    عرف المشاهد: مـصفوفة[مؤشر[دالة(): ثـنائي]]؛
    عرف تسلسل_الاشتراك: صـحيح = 0؛

    عملية هذا.هيئ(تسلسل_الاشتراك: صـحيح، عدد_المشاهد: صـحيح، المشاهد: ...مؤشر[دالة(): ثـنائي]) {
        هذا.تسلسل_الاشتراك = تسلسل_الاشتراك؛
        بينما عدد_المشاهد-- > 0 هذا.المشاهد.أضف(المشاهد~المعطى_التالي[مؤشر[دالة(): ثـنائي]])؛
    }
}

//================
// الدالات العمومية

دالة شاشة_البداية (أظهر_المقدمة: ثـنائي) {
    القائمة_العائمة.عطل()؛
    عرف مشهد:مـشهد؛
    عرف موسيقى: صـوت؛
    عرف أيقونة_الموسيقى: مـلصق؛
    انتظر(مـؤجلات.مـؤجلة[صـحيح].الكل({
        مشهد.حمل_خلفية(نـص("رسوم/شاشة_البداية.png")، 4)،
        موسيقى.هيئ(نـص("أصوات/INTRO.mp3"))،
        أيقونة_الموسيقى.هيئ(نـص("رسوم/لوح_مفاتيح/موسيقى")، 0)،
    }).تجاهل_النتيجة())؛
    أيقونة_الموسيقى.ضع(0، 0)؛
    مشهد.أضف_ملصق(أيقونة_الموسيقى)؛

    إذا أظهر_المقدمة {
        المقدمة1()؛
        حدث_حالة_الموسيقى(موسيقى، أيقونة_الموسيقى)؛
        المقدمة2()؛
    } وإلا {
        حدث_حالة_الموسيقى(موسيقى، أيقونة_الموسيقى)؛
    }

    مشهد.تحديث = مغلفة (موسيقى: كسند، أيقونة_الموسيقى: كسند)&() {
        إذا تحكم.نقرة_للتو {
            إذا تحكم.س >= أيقونة_الموسيقى.س و تحكم.س < أيقونة_الموسيقى.س + أيقونة_الموسيقى.عرض
            و تحكم.ص >= أيقونة_الموسيقى.ص و تحكم.ص < أيقونة_الموسيقى.ص + أيقونة_الموسيقى.طول {
                اقلب_تشغيل_الموسيقى(موسيقى، أيقونة_الموسيقى)؛
            }
        }
    }؛

    أظهر_رويدا(مشهد، 1000)؛
    بينما 1 {
        عرف الآن: صحيح[64] = هات_الختم_الزمني_بالملي()؛
        بينما هات_الختم_الزمني_بالملي() - الآن < 6000 {
            حدث()؛
            إذا تحكم.تفاعل_للتو {
                موسيقى.أوقف(1000)؛
                أخف_رويدا(مشهد، 1000)؛
                اقطع 2؛
            }
            إذا هل_نقر_في_الوسط() {
                موسيقى.أوقف(1000)؛
                أخف_رويدا(مشهد، 1000)؛
                اقطع 2؛
            }
        }
        أخف_رويدا(مشهد، 1000)؛
        فريق_العمل()؛
        أظهر_رويدا(مشهد، 1000)؛
    }؛

    إذا هل_من_لعبة_سابقة() {
        شاشة_الإكمال()؛
    } وإلا {
        حدد_موضع_التشغيل_الرئيسي(نـص("القصة"))؛
    }
}؛


دالة حدث_حالة_الموسيقى(موسيقى: سند[صـوت]، أيقونة_الموسيقى: سند[مـلصق]) {
    إذا هات_حالة_تشغيل_الموسيقى() {
        موسيقى.شغل(1)؛
        أيقونة_الموسيقى.الحالي = 0؛
    } وإلا {
        موسيقى.أوقف(1000)؛
        أيقونة_الموسيقى.الحالي = 1؛
    }
}


دالة اقلب_تشغيل_الموسيقى(موسيقى: سند[صـوت]، أيقونة_الموسيقى: سند[مـلصق]) {
    حدد_حالة_تشغيل_الموسيقى(ليس هات_حالة_تشغيل_الموسيقى())؛
    حدث_حالة_الموسيقى(موسيقى، أيقونة_الموسيقى)؛
}


دالة هل_نقر_في_الوسط(): ثـنائي {
    أرجع تحكم.نقرة_للتو و تحكم.س >= 120 و تحكم.س < _عرض_النافذة_ - 120
    و تحكم.ص >= 120 و تحكم.ص < _طول_النافذة_ - 120؛
}


دالة المقدمة1() {
    عرف مشهد:مـشهد؛
    انتظر(مشهد.حمل_خلفية(نـص("رسوم/مقدمة1.png")، 0))؛
    أظهر_رويدا(مشهد، 1000)؛
    بينما 1 {
        إذا هل_نقر_في_الوسط() أو تحكم.تفاعل_للتو اقطع؛
        حدث(1)؛
    }
    أخف_رويدا(مشهد، 1000)؛
}


دالة المقدمة2() {
    عرف مشهد:مـشهد؛
    انتظر(مشهد.حمل_خلفية(نـص("رسوم/مقدمة2.png")، 0))؛
    أظهر_رويدا(مشهد، 1000)؛
    عرف الآن: صحيح[64] = هات_الختم_الزمني_بالملي()؛
    بينما هات_الختم_الزمني_بالملي() - الآن < 2500 {
        حدث()؛
        إذا هل_نقر_في_الوسط() أو تحكم.تفاعل_للتو اقطع؛
    }
    أخف_رويدا(مشهد، 1000)؛
}


دالة هل_من_لعبة_سابقة(): ثـنائي {
    عرف موضع: نـص = هات_موضع_التشغيل_الرئيسي()؛
    أرجع موضع == "القائمة" أو موضع == "الحلم1" أو موضع == "الحلم2" أو موضع == "الحلم3"؛
}


دالة شاشة_الإكمال {
    عرف مشهد:مـشهد؛

    عرف خيارات: مـصفوفة[مـلصق]({ مـلصق()، مـلصق() })؛
    انتظر(مـؤجلات.مـؤجلة[صـحيح].الكل({
        مشهد.حمل_خلفية(نـص("رسوم/إكمال_خلفية.png")، 0)،
        خيارات(0).هيئ(نـص("رسوم/إكمال_تكملة")، 0، 278، 186)،
        خيارات(1).هيئ(نـص("رسوم/إكمال_جديد")، 0، 382، 446)
    }).تجاهل_النتيجة())؛

    أظهر_رويدا(مشهد، 1000)؛

    مشهد.أضف_ملصقات(خيارات)؛

    عرف ص :صحيح = 1؛
    بينما 1 {
        إذا تحكم.أسفل_للتو و ص < 2 ص += 1؛
        إذا تحكم.أعلى_للتو و ص > 1 ص -= 1؛
        عرف تفاعل_للتو: ثـنائي = تحكم.تفاعل_للتو؛

        عرف م: صحيح؛

        لكل م = 0، م < 2، ++م {
            إذا تحكم.س >= خيارات(م).س
                و تحكم.س < خيارات(م).س + خيارات(م).عرض
                و تحكم.ص >= خيارات(م).ص
                و تحكم.ص < خيارات(م).ص + خيارات(م).طول
            {
                ص = م + 1؛
            }
        }

        إذا تحكم.نقرة_للتو {
            لكل م = 0، م < 2، ++م {
                إذا تحكم.س >= خيارات(م).س
                    و تحكم.س < خيارات(م).س + خيارات(م).عرض
                    و تحكم.ص >= خيارات(م).ص
                    و تحكم.ص < خيارات(م).ص + خيارات(م).طول
                {
                    ص = م + 1؛
                    تفاعل_للتو = 1؛
                }
            }
        }

        لكل م = 0، م < 2، ++م {
            إذا م+1 == ص خيارات(م).شفافية = 255 وإلا خيارات(م).شفافية = 0؛
        }

        إذا تفاعل_للتو و ص == 1 {
            اقطع؛
        }؛

        إذا تفاعل_للتو و ص == 2 {
            حدد_موضع_التشغيل_الرئيسي(نـص("القصة"))؛
            اقطع؛
        }؛

        حدث(1)؛
    }
    أخف_رويدا(مشهد، 1000)؛
}


دالة فريق_العمل {
    عرف أسماء_الملفات: مـصفوفة[نـص]({
        نـص("رسوم/فريق_العمل/سيناريو.png")،
        نـص("رسوم/فريق_العمل/رسوم.png")،
        نـص("رسوم/فريق_العمل/برمجة.png")،
        نـص("رسوم/فريق_العمل/برمجة_الأدوات.png")،
        نـص("رسوم/فريق_العمل/برمجة_النسخة_الأصلية.png")،
        نـص("رسوم/فريق_العمل/الأصوات.png")،
        نـص("رسوم/فريق_العمل/موسيقى.png")،
        نـص("رسوم/فريق_العمل/إشراف.png")،
    })؛
    عرف مشهد:مـشهد؛
    عرف ع: صحيح؛
    لكل ع = 0، ع < أسماء_الملفات.هات_الطول()، ++ع {
        انتظر(مشهد.حمل_خلفية(أسماء_الملفات(ع)، 0))؛
        أظهر_رويدا(مشهد، 1000)؛
        عرف الآن: صحيح[64] = هات_الختم_الزمني_بالملي()؛
        بينما هات_الختم_الزمني_بالملي() - الآن < 3000 {
            حدث()؛
            إذا هل_نقر_في_الوسط() أو تحكم.تفاعل_للتو {
                أخف_رويدا(مشهد، 1000)؛
                ارجع؛
            }
        }
        أخف_رويدا(مشهد، 1000)؛
    }
}


دالة القصة() {
    إذا ليس في_الفصل() ارجع؛
    إذا ليس في_غرفة_النوم() ارجع؛
    إذا ليس خارج_مدينة_الرياضيات() ارجع؛
    إذا ليس في_مدينة_الرياضيات() ارجع؛
    حدد_موضع_التشغيل_الرئيسي(نـص("القائمة"))؛
}


دالة القائمة (): ثـنائي {
    القائمة_العائمة.مكن_العودة()؛
    عرف مشهد:مـشهد؛

    عرف أحلام: مـصفوفة[مـلصق]({ مـلصق()، مـلصق()، مـلصق() })؛
    انتظر(مـؤجلات.مـؤجلة[صـحيح].الكل({
        مشهد.حمل_خلفية(نـص("رسوم/القائمة/القائمة.png")، 4)،
        أحلام(0).هيئ(نـص("رسوم/القائمة/حلم1")، 4، 118*4، 14*4)،
        أحلام(1).هيئ(نـص("رسوم/القائمة/حلم2")، 4، 118*4، 63*4)،
        أحلام(2).هيئ(نـص("رسوم/القائمة/حلم3")، 4، 118*4، 113*4)،
    }).تجاهل_النتيجة())؛

    أظهر_رويدا(مشهد، 1000)؛

    مشهد.أضف_ملصقات(أحلام)؛
    عرف ص :صحيح = 1؛
    بينما 1 {
        إذا تحكم.أسفل_للتو و ص < 3 ص += 1؛
        إذا تحكم.أعلى_للتو و ص > 1 ص -= 1؛
        عرف تفاعل_للتو: ثـنائي = تحكم.تفاعل_للتو؛

        عرف م: صحيح؛

        لكل م = 0، م < 3، ++م {
            إذا تحكم.س >= أحلام(م).س
                و تحكم.س < أحلام(م).س + أحلام(م).عرض
                و تحكم.ص >= أحلام(م).ص
                و تحكم.ص < أحلام(م).ص + أحلام(م).طول
            {
                ص = م + 1؛
            }
        }

        إذا تحكم.نقرة_للتو {
            لكل م = 0، م < 3، ++م {
                إذا تحكم.س >= أحلام(م).س
                    و تحكم.س < أحلام(م).س + أحلام(م).عرض
                    و تحكم.ص >= أحلام(م).ص
                    و تحكم.ص < أحلام(م).ص + أحلام(م).طول
                {
                    ص = م + 1؛
                    تفاعل_للتو = 1؛
                }
            }
        }

        لكل م = 0، م < 3، ++م {
            إذا م+1 == ص أحلام(م).شفافية = 255 وإلا أحلام(م).شفافية = 0؛
        }

        إذا تفاعل_للتو و ص == 1 {
            حدد_موضع_التشغيل_الرئيسي(نـص("الحلم1"))؛
            حدد_موضع_التشغيل_الفرعي(0)؛
            أخف_رويدا(مشهد، 1000)؛
            ارجع 1؛
        }؛
        
        إذا تفاعل_للتو و ص == 2 {
            حدد_موضع_التشغيل_الرئيسي(نـص("الحلم2"))؛
            حدد_موضع_التشغيل_الفرعي(0)؛
            أخف_رويدا(مشهد، 1000)؛
            ارجع 1؛
        }؛
        
        
        إذا تفاعل_للتو و ص == 3 {
            حدد_موضع_التشغيل_الرئيسي(نـص("الحلم3"))؛
            حدد_موضع_التشغيل_الفرعي(0)؛
            أخف_رويدا(مشهد، 1000)؛
            ارجع 1؛
        }؛

        حدث(1)؛
        تحقق_من_التجاوز_والعودة[]؛
    }
    أرجع 1؛
}؛


دالة الحلم(ح: سند[حـلم]) {
    عرف ت: صـحيح = هات_موضع_التشغيل_الفرعي()؛
    إذا ت < 0 أو ت >= ح.المشاهد.هات_الطول() ت = 0؛
    بينما ت < ح.المشاهد.هات_الطول() {
        إذا ت >= ح.تسلسل_الاشتراك تحقق_من_الإشتراك()؛
        إذا ح.المشاهد(ت)() {
            ++ت؛
            حدد_موضع_التشغيل_الفرعي(ت)؛
        } وإلا {
            اقطع؛
        }
    }
    حدد_موضع_التشغيل_الرئيسي(نـص("القائمة"))؛
}

//============
// تجهيز الأحلام

دالة جهز_الحلم1(حلم: سند[حـلم]) {
    حلم.هيئ(4، {
        في_حفل_الزواج~مؤشر،
        على_الطريق11~مؤشر،
        في_القصر1~مؤشر،
        مرحلة_11~مؤشر،
        // مرحلة_12~مؤشر،
        // في_الحديقة1~مؤشر،
        // مرحلة13~مؤشر،
        // في_الحديقة2~مؤشر،
        // في_القصر2~مؤشر
    })؛
}


دالة جهز_الحلم2(حلم: سند[حـلم]) {
    حلم.هيئ(2، {
        // في_المدينة1~مؤشر،
        // في_المدينة2~مؤشر،
        // مرحلة21~مؤشر،
        // مرحلة_22~مؤشر،
        // مرحلة_23~مؤشر،
        // في_المدينة3~مؤشر
    })؛
}


دالة جهز_الحلم3(حلم: سند[حـلم]) {
    حلم.هيئ(3، {
        // في_قصر_القسمة~مؤشر،
        // خارج_قصر_الصفر~مؤشر،
        // على_الطريق~مؤشر،
        // مرحلة_31~مؤشر،
        // مرحلة_32~مؤشر،
        // مرحلة_33~مؤشر
    })؛
}

//==============
// مدخل البرنامج

دالة سيف (تجاوز_المقدمة: ثـنائي) {
    جهز_السواد()؛
    جهز_نافذة_إعادة_المحاولة()؛
    عرف قائمة_عائمة: قـائمة_عائمة؛
    القائمة_العائمة~عطل_التتبع = قائمة_عائمة؛
    انتظر(القائمة_العائمة.هيئ())؛
    عرف حلم1: حـلم؛
    عرف حلم2: حـلم؛
    عرف حلم3: حـلم؛
    جهز_الحلم1(حلم1)؛
    جهز_الحلم2(حلم2)؛
    جهز_الحلم3(حلم3)؛

    إذا ليس تجاوز_المقدمة {
        شاشة_البداية(1)؛
    }

    بينما 1 {
        عرف م: نـص = هات_موضع_التشغيل_الرئيسي()؛

        إذا م == "البداية" شاشة_البداية(0)
        وإلا إذا م == "القصة" القصة()
        وإلا إذا م == "القائمة" القائمة()
        وإلا إذا م == "الحلم1" الحلم(حلم1)
        وإلا إذا م == "الحلم2" الحلم(حلم2)
        وإلا إذا م == "الحلم3" الحلم(حلم3)
        وإلا {
            حدد_موضع_التشغيل_الرئيسي(نـص("البداية"))؛
        }
    }
}
