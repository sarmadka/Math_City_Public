عرف نافذة: مؤشر[طـوب.نـافذة]؛

دالة تهيئة {
    إذا طـوب.هيئ(طـوب._تهيئة_عصا_التحكم_) != 0 {
        مـتم.طـرفية.اطبع("فشلت تهئية مكتبة ط.و.ب.\ج")؛
        نـظام.اخرج(1)؛
    }؛

    إذا طـوب.صـورة.هيئ(طـوب.صـورة._تهيئة_بنج_) != طـوب.صـورة._تهيئة_بنج_ {
        مـتم.طـرفية.اطبع("فشلت تهيئة مكتبة صورة ط.و.ب\ج")؛
        نـظام.اخرج(1)؛
    }؛

    إذا طـوب.سـمعي.هيئ(44100، طـوب.سـمعي.بـنية._مبدئي_، 2، 2048) < 0 {
        مـتم.طـرفية.اطبع("فشلت تهيئة مكتبة السمعيات\ج")؛
        نـظام.اخرج(1)؛
    }؛

    طـوب.كـتابة.هيئ()؛

    نافذة = طـوب.نـافذة.أنشئ("سيف في مدينة الرياضيات"، 0، 0، _عرض_النافذة_، _طول_النافذة_، 0)؛
    إذا نافذة == 0 {
        مـتم.طـرفية.اطبع("فشل إنشاء النافذة\ج")؛
        نـظام.اخرج(1)؛
    }؛
    حدد_ملء_الشاشة(نافذة، 0x00001001)؛

    طـوب.عين_تلميح(طـوب.تـلميحات._رسوم_جودة_التحجيم_، "linear")؛

    الرسام = طـوب.رسـام.أنشئ(نافذة، -1، طـوب.رسـام.عـلامات._مسرع_)؛
    إذا الرسام == 0 {
        مـتم.طـرفية.اطبع("فشل إنشاء الرسام\ج")؛
        نـظام.اخرج(1)؛
    }؛
    طـوب.رسـام.عين_حجم_رقعة_الرسم(الرسام، _عرض_النافذة_، _طول_النافذة_)؛

    ريـاضيات.جهز_العشوائية(وقـت.هات_الختم_الزمني(0))؛

    طـوب.عين_تلميح(طـوب.تـلميحات._رسوم_جودة_التحجيم_، "nearest")؛

    حمل_الموارد_العامة()؛
}


دالة هات_أبعاد_النافذة (عرض: سند[صـحيح]، طول: سند[صـحيح]) {
    طـوب.نـافذة.هات_الأبعاد(نافذة، عرض~مؤشر، طول~مؤشر)؛
}

دالة حمل_الموارد_العامة {
    عرف _اسم_ملف_الخط_: "خطوط/Jawadtaut-1.otf"؛
    خط_فقاعات_الكلام = طـوب.كـتابة.افتح_خط(_اسم_ملف_الخط_، 45)؛
    إذا خط_فقاعات_الكلام == 0 {
        طـرفية.اطبع("فشل تحميل خط فقاعات الكلام.\ج")؛
        نـظام.اخرج(1)؛
    }
}


دالة ابدأ_رسم_النقش (نقش: سند[نـقش_متعدد]، نظف: ثنائي، حالة: صحيح) {
    إذا نقش.نقوش.هات_الطول() == 0 {
        طـرفية.اطبع("لم يُهيأ النقش بعد.\ج")؛
        نـظام.اخرج(1)؛
    }
    طـوب.رسـام.حدد_المرسم(الرسام، نقش.نقوش(حالة))؛
    إذا نظف {
        طـوب.رسـام.عين_لون_الرسم(الرسام، 0، 0، 0، 0)؛
        طـوب.رسـام.نظف(الرسام)؛
    }
}


دالة أنه_رسم_النقش {
    طـوب.رسـام.حدد_المرسم(الرسام، 0)؛
}


دالة عين_لون_الرسم (لون: سند[لـون]) {
    عين_لون_الرسم(لون.ح، لون.خ، لون.ز، لون.ش)؛
}


دالة عين_لون_الرسم (ح: صـحيح، خ: صـحيح، ز: صـحيح، ش: صـحيح) {
    طـوب.رسـام.عين_لون_الرسم(الرسام، ح، خ، ز، ش)؛
}


دالة ارسم_مستطيلا_مليئا (س1: صحيح، ص1: صحيح، س2: صحيح، ص2: صحيح) {
    عرف مستطيل: طـوب.مـستطيل(س1، ص1، س2 - س1 + 1، ص2 - ص1 + 1)؛
    طـوب.رسـام.املأ_مستطيل(الرسام، مستطيل~مؤشر)؛
}


دالة فرغ_مستطيلا (س1: صحيح، ص1: صحيح، س2: صحيح، ص2: صحيح) {
    عرف مستطيل: طـوب.مـستطيل(س1، ص1، س2 - س1 + 1، ص2 - ص1 + 1)؛
    طـوب.رسـام.عين_لون_الرسم(الرسام، 0، 0، 0، 0)؛
    طـوب.رسـام.عين_نمط_المزج(الرسام، طـوب.نـمط_المزج._بلا_)؛
    طـوب.رسـام.املأ_مستطيل(الرسام، مستطيل~مؤشر)؛
}


دالة حمل_صوتا(اسم_الملف: مـؤشر_محارف): سـندنا[مـؤجلات.مـؤجلة[سـندنا[صـوت]]] {
    عرف صوت: سـندنا[صـوت]؛
    صوت.أنشئ()؛
    أرجع مـؤجلات.مـؤجلة[سـندنا[صـوت]].أنشئ().{
        قرر(صوت)؛
    }؛
}


دالة حدث {
    حدث(0)؛
}


دالة حدث (انتظر: ثنائي) {
    عرف حدث: طـوب.حـدث؛
    بينما طـوب.استشعر_حدث(حدث~مؤشر) != 0 {
      إذا حدث.نوع == طـوب.حـدث._زر_للأسفل_ {
        إذا حدث~مؤشر~مثل[مؤشر[طـوب.حـدث_مفاتيح]]~محتوى.كود_المسح == طـوب.لـوح_المفاتيح.كـود_المسح.ESCAPE نـظام.اخرج(0)؛
      }
      إذا حدث.نوع == طـوب.حـدث._خروج_ نـظام.اخرج(0)؛
    }؛

    ارسم_النافذة()؛

    نـظام.نم(1000)؛

    تحكم.حدث()؛
    حدث_النافذة()؛
}


دالة حدث_النافذة {
    عرف ن: صحيح؛
    لكل ن = 0، ن < مـشهد.المشاهد.هات_الطول()، ++ن {
        مـشهد.المشاهد(ن).حدث()؛
    }
}


دالة ارسم_النافذة {
    طـوب.رسـام.عين_لون_الرسم(الرسام، 0، 0، 0، 255)؛
    طـوب.رسـام.نظف(الرسام)؛

    عرف ن: صحيح؛
    لكل ن = 0، ن < مـشهد.المشاهد.هات_الطول()، ++ن {
        مـشهد.المشاهد(ن).ارسم()؛
    }

    طـوب.رسـام.اعرض(الرسام)؛
}


صنف خـتم_زمني {
    عرف ثواني: صحيح[64]؛
    عرف نانو_ثواني: صحيح[64]؛
}


دالة هات_الختم_الزمني_بالملي (): صحيح[64] {
    عرف ختم_زمني: خـتم_زمني؛
    هات_الختم_الزمني_بالنانو(0، ختم_زمني)؛
    أرجع ختم_زمني.ثواني * 1000 + ختم_زمني.نانو_ثواني ÷ 1000000؛
}


دالة تمهل (ميلي_ثواني: صحيح[64]) {
    عرف ختم_زمني: خـتم_زمني؛
    هات_الختم_الزمني_بالنانو(0، ختم_زمني)؛
    عرف البداية: صحيح[64] = ختم_زمني.ثواني * 1000 + ختم_زمني.نانو_ثواني ÷ 1000000؛
    بينما 1 {
        هات_الختم_الزمني_بالنانو(0، ختم_زمني)؛
        عرف الآن: صحيح[64] = ختم_زمني.ثواني * 1000 + ختم_زمني.نانو_ثواني ÷ 1000000؛
        إذا الآن - البداية >= ميلي_ثواني اقطع؛
        حدث()؛
        إذا القائمة_العائمة.التجاوز_مفعل أو القائمة_العائمة.العودة_مفعلة ارجع؛
    }
}


دالة انتظر [صـنف_البيانات: صنف = صـحيح] (م: سـندنا[مـؤجلات.مـؤجلة[صـنف_البيانات]]): صـنف_البيانات {
    بينما 1 {
        إذا م.الحالة != مـؤجلات.حـالة._جديد_ اقطع؛
        مـتم.نـظام.نم(15000)؛
        حدث()؛
    }
    أرجع م.النتيجة؛
}

عرف انتظر_مع_التحديث: لقب انتظر؛

دالة تحقق_من_الإشتراك {
}

عرف موضع_التشغيل_الرئيسي: نـص؛

دالة هات_موضع_التشغيل_الرئيسي(): نـص {
    أرجع موضع_التشغيل_الرئيسي؛
}

دالة حدد_موضع_التشغيل_الرئيسي(م: نـص) {
    موضع_التشغيل_الرئيسي = م؛
}

عرف موضع_التشغيل_الثانوي: صـحيح؛

دالة هات_موضع_التشغيل_الفرعي(): صـحيح {
    أرجع موضع_التشغيل_الثانوي؛
}

دالة حدد_موضع_التشغيل_الفرعي(م: صـحيح) {
    موضع_التشغيل_الثانوي = م؛
}

عرف حالة_تشغيل_الموسيقى: ثـنائي؛

دالة هات_حالة_تشغيل_الموسيقى(): ثـنائي {
    أرجع حالة_تشغيل_الموسيقى؛
}

دالة حدد_حالة_تشغيل_الموسيقى(م: ثـنائي) {
    حالة_تشغيل_الموسيقى = م؛
}
