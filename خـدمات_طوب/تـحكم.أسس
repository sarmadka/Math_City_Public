صنف تـحكم {
    عرف أعلى: ثنائي = 0؛
    عرف أعلى_للتو: ثنائي = 0؛
    
    عرف يمين: ثنائي = 0؛
    عرف يمين_للتو: ثنائي = 0؛
    
    عرف يسار: ثنائي = 0؛
    عرف يسار_للتو: ثنائي = 0؛

    عرف أسفل: ثنائي = 0؛
    عرف أسفل_للتو: ثنائي = 0؛

    عرف تفاعل: ثنائي = 0؛
    عرف تفاعل_للتو: ثنائي = 0؛

    عرف محو: ثنائي = 0؛
    عرف محو_للتو: ثنائي = 0؛

    عرف الرقم: صحيح = -1؛
    عرف الرقم_مكبوس: ثنائي = 0؛

    عرف نقرة: ثـنائي = 0؛
    عرف نقرة_للتو: ثـنائي = 0؛

    عرف س: صـحيح = 0؛
    عرف ص: صـحيح = 0؛

    عملية هذا.تفاعل_أو_نقرة_للتو: ثـنائي {
        أرجع هذا.تفاعل_للتو أو هذا.نقرة_للتو؛
    }

    عرف عند_النقر: مغلفة ()؛

    عرف حالة_المفاتيح: مؤشر[مصفوفة[طبيعي[8]]]؛

    عملية هذا.حدث() {
        هذا.حالة_المفاتيح = طـوب.لـوح_المفاتيح.هات_الحالة(0)؛

        هذا.حدث_مفتاح_الأرقام()؛
        هذا.حدث_مفتاح(طـوب.لـوح_المفاتيح.كـود_المسح.UP، هذا.أعلى، هذا.أعلى_للتو)؛
        هذا.حدث_مفتاح(طـوب.لـوح_المفاتيح.كـود_المسح.DOWN، هذا.أسفل، هذا.أسفل_للتو)؛
        هذا.حدث_مفتاح(طـوب.لـوح_المفاتيح.كـود_المسح.RIGHT، هذا.يمين، هذا.يمين_للتو)؛
        هذا.حدث_مفتاح(طـوب.لـوح_المفاتيح.كـود_المسح.LEFT، هذا.يسار، هذا.يسار_للتو)؛
        هذا.حدث_مفتاح(طـوب.لـوح_المفاتيح.كـود_المسح.SPACE، هذا.تفاعل، هذا.تفاعل_للتو)؛
        هذا.حدث_مفتاح(طـوب.لـوح_المفاتيح.كـود_المسح.BACKSPACE، هذا.محو، هذا.محو_للتو)؛

        هذا.نقرة_للتو = 0؛
        عرف حالة_الأزرار: طـبيعي = طـوب.فـأرة.هات_الحالة(هذا.س~مؤشر، هذا.ص~مؤشر)؛
        إذا حالة_الأزرار & 1 != 0 {
            إذا هذا.نقرة == 0 {
                هذا.نقرة_للتو = 1؛
                إذا ليس هذا.عند_النقر.أهو_عدم() هذا.عند_النقر()؛
            }
            هذا.نقرة = 1؛
        } وإلا هذا.نقرة = 0؛

        // تحويل موقع المؤشر من إحداثيات الشاشة إلى إحداثيات لوحة الرسم.
        عرف عرض: صـحيح؛
        عرف طول: صـحيح؛
        هات_أبعاد_النافذة(عرض، طول)؛
        عرف نسبة_العرض: عـائم = _عرض_النافذة_~مثل[عـائم] ÷ عرض؛
        عرف نسبة_الطول: عـائم = _طول_النافذة_~مثل[عـائم] ÷ طول؛
        عرف النسبة: عـائم؛
        إذا نسبة_العرض >= نسبة_الطول النسبة = نسبة_العرض
        وإلا النسبة = نسبة_الطول؛
        عرف بداية_س: صـحيح = (عرض - _عرض_النافذة_ ÷ النسبة) ÷ 2؛
        عرف بداية_ص: صـحيح = (طول - _طول_النافذة_ ÷ النسبة) ÷ 2؛
        هذا.س = (هذا.س - بداية_س) * النسبة؛
        هذا.ص = (هذا.ص - بداية_ص) * النسبة؛
    }

    عملية هذا.حدث_مفتاح (رمز_المفتاح: صحيح، متغير: سند[ثنائي]، متغير_للتو: سند[ثنائي]) {
        متغير_للتو = 0؛
        إذا هذا.حالة_المفاتيح~محتوى(رمز_المفتاح) != 0 {
            إذا متغير == 0 متغير_للتو = 1؛
            متغير = 1؛
        } وإلا {
            متغير = 0؛
        }
    }
    
    عملية هذا.حدث_مفتاح_الأرقام() {
        هذا.الرقم = -1؛
        عرف م: صحيح؛
        لكل م = طـوب.لـوح_المفاتيح.كـود_المسح.NUM_1، م <= طـوب.لـوح_المفاتيح.كـود_المسح.NUM_0، ++م {
            إذا هذا.حالة_المفاتيح~محتوى(م) != 0 {
                إذا ليس هذا.الرقم_مكبوس {
                    // حدد قيمة جديدة للرقم
                    هذا.الرقم = 1 + م - طـوب.لـوح_المفاتيح.كـود_المسح.NUM_1؛
                    إذا هذا.الرقم == 10 هذا.الرقم = 0؛
                }
                هذا.الرقم_مكبوس = 1؛
                ارجع؛
            }
        }
        هذا.الرقم_مكبوس = 0؛
    }
}

