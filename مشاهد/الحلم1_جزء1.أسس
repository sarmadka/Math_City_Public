عرف مواقع_أرقام_س:مـصفوفة[صحيح]({8*4، 20*4، 36*4، 52*4})؛
عرف مواقع_أرقام_ص:مـصفوفة[صحيح]({2*4، 11*4، 20*4، 29*4، 38*4})؛
عرف مواقع_علامات_س:مـصفوفة[صحيح]({9*4، 24*4، 40*4})؛
عرف مواقع_علامات_ص:مـصفوفة[صحيح]({1*4، 10*4، 19*4، 28*4، 37*4})؛
عرف أرقام_اللغز: مصفوفة[مصفوفة[صحيح، 4]، 5]؛
عرف العلامات: مصفوفة[مصفوفة[صحيح، 2]، 5]؛
عرف العلامات_الجديدة: مصفوفة[مصفوفة[صحيح، 2]، 5]؛

دالة مرحلة_11 (): ثنائي {
    //__________________________________
    // تجهيز الملصقات والأصوات
    //__________________________________
    عرف مشهد: مـشهد؛
    عرف غزال: مـلصق؛
    عرف وجه_الغزال: مـلصق؛
    عرف وجه_الواحد: مـلصق؛
    عرف وجه_الثمانية: مـلصق؛
    عرف وجه_سيف: مـلصق؛
    عرف باب: مـلصق؛
    عرف مقبض_الباب: مـلصق؛
    عرف الزهور: مـلصق؛
    عرف أرقام: نـقش_متعدد؛
    عرف علامات: نـقش_متعدد؛
    عرف دخان: دخـان؛
    عرف المؤشر: مـلصق؛
    عرف لوح_مفاتيح: لـوح_مفاتيح؛

    عرف صوت_مقبض_الباب: صـوت؛
    عرف صوت_الباب: صـوت؛

    انتظر(مـؤجلات.مـؤجلة[صـحيح].الكل({
        مشهد.حمل_خلفية(نـص("رسوم/الحلم1_الجزء1/مرحلة_القفص.png")، 4)،
        غزال.هيئ(نـص("رسوم/الحلم1_الجزء1/غزال")، 4، 47*4، 83*4)،
        وجه_الغزال.هيئ(نـص("رسوم/الحلم1_الجزء1/وجه_الغزال")، 4، 67*4، 91*4)،
        وجه_الواحد.هيئ(نـص("رسوم/الحلم1_الجزء1/واحد")، 4، 236*4، 108*4)،
        وجه_الثمانية.هيئ(نـص("رسوم/الحلم1_الجزء1/ثمانية")، 4، 288*4، 117*4)،
        وجه_سيف.هيئ(نـص("رسوم/الحلم1_الجزء1/وجه_سيف")، 4، 193*4، 110*4)،
        باب.هيئ(نـص("رسوم/الحلم1_الجزء1/باب11")، 4، 32*4، 78*4)،
        مقبض_الباب.هيئ(نـص("رسوم/الحلم1_الجزء1/مقبض_الباب")، 4، 150*4، 94*4)،
        الزهور.هيئ(نـص("رسوم/الحلم1_الجزء1/زهور")، 4، 47*4، 176*4)،
        أرقام.هيئ(نـص("رسوم/الحلم1_الجزء1/أرقام")، 4)،
        علامات.هيئ(نـص("رسوم/الحلم1_الجزء1/علامات")، 4)،
        دخان.هيئ()،
        المؤشر.هيئ(نـص("رسوم/مؤشر")، 4)،
        صوت_مقبض_الباب.هيئ(نـص("أصوات/الحلم1_الجزء1/مقبض_الباب11.wav"))،
        صوت_الباب.هيئ(نـص("أصوات/الحلم1_الجزء1/باب.mp3"))
        لوح_مفاتيح.هيئ(800، 100، 1)
    }).تجاهل_النتيجة())؛

    مشهد.أضف_ملصقات({ غزال، باب، الزهور، مقبض_الباب، وجه_الواحد، وجه_الغزال، وجه_سيف، وجه_الثمانية})؛
    وجه_الغزال.شفافية=0؛
    المؤشر.شفافية=0؛

    //__________________________________
    // تجهيز اللغز
    //__________________________________
    عرف ه : صحيح؛
    عرف ن : صحيح؛
    عرف ن1 : صحيح؛
    لكل ه=0، ه < 5، ه++ {
        أرقام_اللغز(ه)(3) = مـتم.ريـاضيات.عشوائي() % 10؛
        أرقام_اللغز(ه)(2) = مـتم.ريـاضيات.عشوائي() % 10؛
        أرقام_اللغز(ه)(1) = مـتم.ريـاضيات.عشوائي() % 10؛
        العلامات(ه)(1) = مـتم.ريـاضيات.عشوائي() % 2؛
        العلامات(ه)(0) = مـتم.ريـاضيات.عشوائي() % 2؛
        العلامات_الجديدة(ه)(0) = العلامات_الجديدة(ه)(1)=3؛
        ن = أرقام_اللغز(ه)(3)
            + ريـاضيات.ارفع(-1.0، العلامات(ه)(1)~مثل[عائم]) * أرقام_اللغز(ه)(2)
            + ريـاضيات.ارفع(-1.0، العلامات(ه)(0)~مثل[عائم]) * أرقام_اللغز(ه)(1)؛
        ن1=أرقام_اللغز(ه)(3)
            + ريـاضيات.ارفع(-1.0، العلامات(ه)(1)~مثل[عائم]) * أرقام_اللغز(ه)(2)؛
        إذا ن<0 أو ن1<0 {
            ه--؛
        } وإلا {
            أرقام_اللغز(ه)(0)=ن؛
        }؛
    }؛

    //__________________________________
    // تجهيز اللوح
    //__________________________________
    عرف لوح :مـلصق(55*4، 45*4)؛
    عرف ت:صحيح؛
    ابدأ_رسم_النقش(لوح، 1)؛
    لكل ه=0، ه<5، ه++ {
        لكل ت=0، ت<4، ت++ {
            ارسم_الرقم(أرقام، مواقع_أرقام_س(ت)، مواقع_أرقام_ص(ه)، أرقام_اللغز(ه)(ت))؛
        }؛
    }؛
    لكل ه=0، ه<5، ه++ {
        علامات.اطمغ(مواقع_علامات_س(0)، مواقع_علامات_ص(ه)، 255، 2)؛
        علامات.اطمغ(مواقع_علامات_س(1)، مواقع_علامات_ص(ه)، 255، 3)؛
        علامات.اطمغ(مواقع_علامات_س(2)، مواقع_علامات_ص(ه)، 255، 3)؛
    }؛
    أنه_رسم_النقش()؛
    لوح.ضع(93*4، 85*4)؛
    مشهد.أضف_ملصق(لوح)؛
    مشهد.أضف_ملصق(المؤشر)؛

    //__________________________________
    // الحوار
    //__________________________________

    القائمة_العائمة.مكن_التجاوز()؛
    أظهر_رويدا(مشهد، 1000)؛

    تمهل_مع_تمكين_العودة[1000]؛

    إذا ليس القائمة_العائمة.التجاوز_مفعل {
        اعرض_محادثة(مشهد، مـصفوفة_كلام({
            كـلام(
                "أصوات/الحلم1_الجزء1/غزال1-1.wav"،
                نـص("اهي، اهي، اهي")،
                88*4، 95*4، 30*4، 30، 500
            )،
            كـلام(
                "أصوات/الحلم1_الجزء1/سيف1-3.mp3"،
                نـص("يا للمسكين.. من الذي احتجزك\جفي هذا القفص؟")،
                190*4، 108*4, 30*4, 120، وجه_سيف، 0، 1، 500
            )
        }))؛
        تحقق_من_العودة[]؛
    }
    باب.الحالي=1؛
    إذا ليس القائمة_العائمة.التجاوز_مفعل {
        اعرض_محادثة(مشهد، مـصفوفة_كلام({
            كـلام(
                "أصوات/الحلم1_الجزء1/غزال1-2.wav"،
                نـص("إنها الساحرة الشريرة التي\جتسكن في الحديقة المجاورة.")،
                88*4، 95*4، 30*4، 30، وجه_الغزال، 0، 1، 500
            )،
            كـلام(
                "أصوات/الحلم1_الجزء1/واحد1-2.wav"،
                نـص("لا تخف، سنخرجك حالًا،\جولكن، كيف يفتح هذا الباب؟")،
                232*4، 106*4، 30*4، 110، وجه_الواحد، 0، 1، 200
            )،
            كـلام(
                "أصوات/الحلم1_الجزء1/غزال1-3.wav"،
                نـص("يجب أن تضعوا علامات الجمع\ج"
                    "والطرح في أماكنها الصحيحة\ج"
                    "ثم تسحبوا قبضة الباب.")،
                88*4، 95*4، 30*4، 30، وجه_الغزال، 0، 1، 500
            )،
        }))؛
        تحقق_من_العودة[]؛
    }

    //__________________________________
    // بداية اللعبة
    //__________________________________

    القائمة_العائمة.مكن_العودة()؛

    عرف المحاولات:صحيح=3؛
    عرف رسم_المحاولات:مـحاولات؛
    انتظر_مع_التحديث(رسم_المحاولات.هيئ())؛
    لوح_مفاتيح.ارسم(مشهد)؛

    ت=ه=0؛
    المؤشر.شفافية=255؛
    بينما 1 {
        رسم_المحاولات.عين(المحاولات، مشهد)؛
        هات_الجواب11(المؤشر، ت، ه، لوح_مفاتيح)؛
        تحقق_من_العودة[]؛

        إذا ت==2 {
            إذا هل_الجواب_صحيح11() {
                القائمة_العائمة.مكن_التجاوز()؛
                المؤشر.شفافية=0؛
                صوت_مقبض_الباب.شغل(0)؛
                باب.الحالي=0؛
                مقبض_الباب.الحالي=1؛
                تمهل_مع_تمكين_التجاوز_والعودة[500]؛
                صوت_الباب.شغل(0)؛
                تمهل_مع_تمكين_التجاوز_والعودة[200]؛
                // تحريك الباب
                عرف ص_باب: صـحيح = باب.ص؛
                عرف ص_مقبض: صـحيح = مقبض_الباب.ص؛
                عرف ص_لوح: صـحيح = لوح.ص؛
                عرف وقت_البداية: صـحيح[64] = هات_الختم_الزمني_بالملي()؛
                بينما 1 {
                    عرف الآن: صـحيح[64] = هات_الختم_الزمني_بالملي()؛
                    عرف ص: صـحيح = (الآن - وقت_البداية) * 400 ÷ 2200؛
                    إذا ص >= 400 {
                        باب.ص = ص_باب - 400؛
                        مقبض_الباب.ص = ص_مقبض - 400؛
                        لوح.ص = ص_لوح - 400؛
                        اقطع؛
                    } وإلا {
                        باب.ص = ص_باب - ص؛
                        مقبض_الباب.ص = ص_مقبض - ص؛
                        لوح.ص = ص_لوح - ص؛
                    }
                    حدث()؛
                    تحقق_من_التجاوز_والعودة[]؛
                }
                //لكل ه=0، ه<400، ه++{
                //    تمهل(5)؛
                //    باب.ص-=1؛
                //    مقبض_الباب.ص-=1؛
                //    لوح.ص-=1؛
                //    حدث()؛
                //}؛

                // إزالة الملصق ثم إعادته ليكون في المقدمة (فوق الأزهار بدل أن يكون خلفها).
                مشهد.أزل_ملصق(غزال)؛
                مشهد.أضف_ملصق(غزال)؛
                غزال.ضع(68*4، 99*4)؛
                غزال.الحالي=1؛
                لوح_مفاتيح.احذف(مشهد)؛
                
                تمهل_مع_تمكين_التجاوز_والعودة[1500]؛
                
                //حوار
                وجه_الغزال.ضع(118*4، 110*4)؛
                اعرض_محادثة(مشهد، مـصفوفة_كلام({
                    كـلام(
                        "أصوات/الحلم1_الجزء1/غزال1-7.wav"،
                        نـص("شكرًا يا أصدقائي،\ج"
                        "ولكن ما الذي أتى بكم\ج"
                        "إلى هذا المكان؟")،
                        132*4، 111*4، 30*4، 45، وجه_الغزال، 2، -1، 500
                    )،    
                    كـلام(
                        "أصوات/الحلم1_الجزء1/ثمانية1-1.wav"،
                        نـص("إننا نبحث عن\ج"
                        "زهرة الشفاء.")،
                        288*4، 116*4، 30*4، 120، وجه_الثمانية، 0، 1، 500
                    )،
                    كـلام(
                        "أصوات/الحلم1_الجزء1/غزال1-8.wav"،
                        نـص("إنها في الحديقة المجاورة\ج"
                        "ولكن الساحرة الشريرة لن\ج"
                        "تسمح لكم بأخذها.")،
                        132*4، 111*4، 30*4، 45، وجه_الغزال، 2، -1، 500
                    )،    
                    كـلام(
                        "أصوات/الحلم1_الجزء1/سيف1-4.mp3"،
                        نـص("ولكننا يجب أن نحاول\ج"
                        "فحياة الأمير والأميرة في خطر!")،
                        190*4، 108*4, 30*4, 120، وجه_سيف، 0، 1، 500
                    )
                }))؛
                تحقق_من_التجاوز_والعودة[]؛
                أخف_رويدا(مشهد، 1000)؛
                ارجع 1؛
            } وإلا {
                // الإجابة خاطئة
                المحاولات--؛
                رسم_المحاولات.عين(المحاولات، مشهد)؛
                صوت_مقبض_الباب.شغل(0)؛
                مقبض_الباب.الحالي=1؛
                تمهل_مع_تمكين_العودة[500]؛
                مقبض_الباب.الحالي=0؛
                إذا المحاولات==2 {
                    اعرض_محادثة(مشهد، مـصفوفة_كلام({
                        كـلام(
                            "أصوات/الحلم1_الجزء1/غزال1-4.wav"،
                            نـص("لقد أخطأتم هذه المرة.\ج"
                             "أرجوكم أن تحاولوا\ج"
                             "مرة اخرى.")،
                            88*4، 95*4، 30*4، 30، وجه_الغزال، 0، 1، 500
                        )،
                    }))؛
                }
                إذا المحاولات==1 {
                    اعرض_محادثة(مشهد، مـصفوفة_كلام({
                        كـلام(
                            "أصوات/الحلم1_الجزء1/غزال1-5.wav"،
                            نـص("لقد أخطأتم ثانية.\ج"
                            "بقيت فرصة واحدة.",)،
                            88*4، 95*4، 30*4، 30، وجه_الغزال، 0، 1، 500
                        )،
                    }))؛
                }؛
                تحقق_من_العودة[]؛
                إذا المحاولات==0 {
                    باب.الحالي=0؛
                    لوح.شفافية=0؛
                    دخان.ارسم(مشهد،93*4، 85*4-60)؛
                    /*دخان.الحالي=0؛
                    دخان.شفافية=255؛
                    لوح.شفافية=0؛
                    تمهل(100)؛
                    دخان.الحالي=1؛
                    تمهل(100)؛
                    دخان.الحالي=2؛
                    تمهل(100)؛
                    دخان.شفافية=0؛*/
                    اعرض_محادثة(مشهد، مـصفوفة_كلام({
                    كـلام(
                    "أصوات/الحلم1_الجزء1/غزال1-6.wav"،
                        نـص("اهي، اهي، اهي،\ج"
                        "سوف أقضي بقية حياتي\ج"
                        "في هذا القفص.")،
                        88*4، 95*4، 30*4، 30، 500
                        )،
                    }))؛
                    أخف_رويدا(مشهد، 1000)؛
                    ارجع 0؛
                }؛
            }
        } وإلا {
            إذا العلامات_الجديدة(ه)(ت)!=0 {
                العلامات_الجديدة(ه)(ت)=0؛            
            } وإلا {
                العلامات_الجديدة(ه)(ت)=1؛
            }؛
            ابدأ_رسم_النقش(لوح، 0)؛أرقام_اللغز
            علامات.اطمغ(مواقع_علامات_س(ت+1)، مواقع_علامات_ص(ه)، 255، العلامات_الجديدة(ه)(ت))؛
            أنه_رسم_النقش()؛
        }؛
        
        حدث()؛
        تحقق_من_العودة[]؛
    }؛
    ارجع 1؛
}؛


دالة ارسم_الرقم (أرقام: سند[نـقش_متعدد]، س: صحيح، ص: صحيح، ر: صحيح) {
    عرف ن : مصفوفة [صحيح، 10]؛
    عرف عدد_المراتب:صحيح=1؛
    عرف قوة:صحيح=1؛
    عرف ه : صحيح؛
    
    لكل ه = 0، ه < 10، ه++ {
        ن(ه)=0؛
    }؛
    
    بينما قوة*10 <= ر {
        قوة = قوة*10؛
        عدد_المراتب++؛
    }؛
    
    لكل ه=عدد_المراتب-1، ه>=0، ه-- {
        ن(ه) = ر÷قوة؛
        ر = ر%قوة؛
        قوة = قوة÷10؛
    }؛
    
    لكل ه = 0، ه < عدد_المراتب، ه++ {
        أرقام.اطمغ(س-(ه+1)*أرقام.عرض، ص، 255، ن(ه))
    }؛
}؛


دالة هات_الجواب11(المؤشر: سند[مـلصق]، سس:سند[صحيح]، صص:سند[صحيح]، لوح_مفاتيح:سند[لـوح_مفاتيح]) {
    عرف س:صحيح؛
    عرف ص:صحيح؛
    عرف م:صحيح؛
    
    بينما 1 {
        إذا سس == 2 {
            س = 150*4؛
            ص = (94+30)*4؛
        } 
        وإلا {
            س = مواقع_علامات_س(سس+1)+93*4؛
            ص = مواقع_علامات_ص(صص)+90*4؛        
        }؛
        المؤشر.ضع(س، ص)؛

        لوح_مفاتيح.حدث()؛

        إذا (تحكم.أسفل_للتو أو لوح_مفاتيح.أسفل_للتو) و صص < 4 صص += 1؛
        إذا (تحكم.أعلى_للتو أو لوح_مفاتيح.أعلى_للتو) و صص > 0 صص -= 1؛
        إذا (تحكم.يمين_للتو أو لوح_مفاتيح.يمين_للتو) و سس < 2 سس += 1؛
        إذا (تحكم.يسار_للتو أو لوح_مفاتيح.يسار_للتو) و سس > 0 سس -= 1؛
        إذا تحكم.تفاعل_للتو أو لوح_مفاتيح.تفاعل_للتو ارجع؛
        حدث(1)؛
        إذا القائمة_العائمة.العودة_مفعلة ارجع؛
    }؛
}؛


دالة هل_الجواب_صحيح11 ():ثنائي {
    عرف ه:صحيح؛
    عرف ن:صحيح؛
    
    لكل ه=0، ه<5، ه++ {
        إذا العلامات_الجديدة(ه)(0)!=0 و العلامات_الجديدة(ه)(0) !=1 {
            ارجع 0؛
        }؛
        إذا العلامات_الجديدة(ه)(1)!=0 و العلامات_الجديدة(ه)(1) !=1 {
            ارجع 0؛
        }؛
        ن = أرقام_اللغز(ه)(3)
          + ريـاضيات.ارفع(-1.0، العلامات_الجديدة(ه)(1)~مثل[عائم])*أرقام_اللغز(ه)(2)
          + ريـاضيات.ارفع(-1.0، العلامات_الجديدة(ه)(0)~مثل[عائم])*أرقام_اللغز(ه)(1)؛
        إذا ن!=أرقام_اللغز(ه)(0) ارجع 0؛
    }؛
    
    ارجع 1؛
}؛

